Attribute VB_Name = "mdlMain"
' XQWMAIN.BAS - Source Code for XiangQi Wizard, Part I
'
' XiangQi Wizard - a Chinese Chess Program (GUI for UCCI Engines)
' Designed by Morning Yellow, Version: 4.41, Last Modified: Nov. 2009
' Copyright (C) 2004-2009 www.elephantbase.net
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 2 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License along
' with this program; if not, write to the Free Software Foundation, Inc.,
' 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.

Option Explicit

' ]有音的MsgBox
Private Type MsgBoxParams
    cbSize As Long
    hwndOwner As Long
    hInstance As Long
    lpszText As String
    lpszCaption As String
    dwStyle As Long
    lpszIcon As Long
    dwContextHelpId As Long
    lpfnMsgBoxCallback As Long
    dwLanguageId As Long
End Type

Private Const MB_USERICON       As Long = 128
Private Const MB_YESNO          As Long = 4
Private Const IDI_QUESTION      As Long = 32514

Private Declare Function MessageBoxIndirectA Lib "USER32.DLL" (ByRef lpMsgBoxParams As MsgBoxParams) As Long

' 全局常量――象棋巫信息
Public Const STRING_VERSION     As String = "4.41"
Public Const STRING_COPYRIGHT   As String = "(C) 2004-2009 www.elephantbase.net"
Public Const TIP_NUM            As Integer = 21   ' 日e月累l目
Public Const QUOTE_NUM          As Integer = 12   ' 名言l目
Public Const ICON_QUOTE         As Integer = 8    ' 名言D
Public Const MAX_ENDGAMES       As Integer = 1024 ' 局的最大l目
Public Const TEMP_FILE_NAME     As String = "查看文本棋V(只x).TXT"
' 全局常量――可x
Public Const AUTO_SAVE          As Boolean = True ' ReadFile
Public Const WRITE_TEMP_FILE    As Boolean = True ' WriteFile
Public Const DRAW_SELECTED      As Boolean = True ' Draw...Square
Public Const DRAW_MATE          As Boolean = True ' Draw...Square
Public Const RESTART_KEEP_LABEL As Boolean = True ' RestartGame
Public Const RESTART_DEBUG_UCCI As Boolean = True ' RestartGame
Public Const MOVE_WITH_ECHO     As Boolean = True ' AddMove
Public Const MOVE_BY_ENGINE     As Boolean = True ' AddMove
Public Const VIEW_BMP_MONO      As Boolean = True ' ViewBitmap
Public Const FLASH_HOME         As Boolean = True ' CopyFlash
Public Const LOAD_WITH_INFO     As Boolean = True ' OpenEngine
Public Const SLAVE_ENGINE       As Boolean = True ' SetEngine / SendEngine / ReceiveEngineLog
Public Const MANUAL_UPDATE      As Boolean = True ' CheckUpdate
' 全局常量――引擎B
Public Const IDLE_UNLOAD        As Integer = 0 ' ]有加d引擎
Public Const IDLE_READY         As Integer = 1 ' 引擎待命(如果到X走棋，那么引擎立即工作)
Public Const IDLE_REST          As Integer = 2 ' 引擎休息(即使到X走棋，引擎也不工作，除非B改)
Public Const IDLE_PONDER        As Integer = 3 ' 後_思考Y束(等待κ肿咄暌徊剑再作相理)
Public Const BUSY_WAIT          As Integer = 4 ' 等待引擎停止思考
Public Const BUSY_ANALYZE       As Integer = 5 ' 引擎以分析方式思考(引擎不倘魏我环剑打_“引擎分析”功能)
Public Const BUSY_THINK         As Integer = 6 ' 引擎正常思考
Public Const BUSY_PONDER        As Integer = 7 ' 引擎後_思考
' 全局常量――棋局Y果
Public Const RESULT_UNKNOWN     As Integer = 0
Public Const RESULT_REDWIN      As Integer = 1
Public Const RESULT_DRAW        As Integer = 2
Public Const RESULT_BLACKWIN    As Integer = 3
' 全局常量――引擎eO置
Public Const LEVEL_GRANDMASTER  As Integer = -5
Public Const LEVEL_MASTER       As Integer = -4
Public Const LEVEL_EXPERT       As Integer = -3
Public Const LEVEL_AMATEUR      As Integer = -2
Public Const LEVEL_BEGINNER     As Integer = -1
Public Const LEVEL_TIMER        As Integer = 0
Public Const LEVEL_DEPTH_MIN    As Integer = 1
Public Const LEVEL_DEPTH_MAX    As Integer = 99
Public Const LEVEL_INFINITE     As Integer = 100
' 全局常量――引擎走棋
Public Const ENGINE_PLAY_NONE   As Integer = 0
Public Const ENGINE_PLAY_RED    As Integer = 1
Public Const ENGINE_PLAY_BLACK  As Integer = 2
Public Const ENGINE_PLAY_TURN   As Integer = 3
Public Const ENGINE_PLAY_RANDOM As Integer = 4
Public Const ENGINE_PLAY_MAX    As Integer = 5
' 全局常量――Flash棋P大小
Public Const FLASHXQ_SMALL      As Integer = 0
Public Const FLASHXQ_MEDIUM     As Integer = 1
Public Const FLASHXQ_LARGE      As Integer = 2
' 全局常量――棋P(小)O置x
Public Const BOARD_S_WOOD       As Integer = 0
Public Const BOARD_S_GREEN      As Integer = 1
Public Const BOARD_S_WHITE      As Integer = 2
Public Const BOARD_S_SHEET      As Integer = 3
Public Const BOARD_S_CANVAS     As Integer = 4
Public Const BOARD_S_DROPS      As Integer = 5
Public Const BOARD_S_CLOUDS     As Integer = 6
Public Const BOARD_S_XQSTUDIO   As Integer = 7
Public Const BOARD_S_MOVESKY    As Integer = 8
Public Const BOARD_S_MRSJ       As Integer = 9
Public Const BOARD_S_ZMBL       As Integer = 10
Public Const BOARD_S_QIANHONG   As Integer = 11
Public Const BOARD_S_MAX        As Integer = 12
' 全局常量――棋子(小)O置x
Public Const PIECES_S_WOOD      As Integer = 0
Public Const PIECES_S_DELICATE  As Integer = 1
Public Const PIECES_S_POLISH    As Integer = 2
Public Const PIECES_S_XQSTUDIO  As Integer = 3
Public Const PIECES_S_MOVESKY   As Integer = 4
Public Const PIECES_S_MRSJ      As Integer = 5
Public Const PIECES_S_ZMBL      As Integer = 6
Public Const PIECES_S_MAX       As Integer = 7
' 全局常量――棋P(大)O置x
Public Const BOARD_L_WOOD       As Integer = 0
Public Const BOARD_L_GREEN      As Integer = 1
Public Const BOARD_L_WHITE      As Integer = 2
Public Const BOARD_L_SHEET      As Integer = 3
Public Const BOARD_L_CANVAS     As Integer = 4
Public Const BOARD_L_DROPS      As Integer = 5
Public Const BOARD_L_QIANHONG   As Integer = 6
Public Const BOARD_L_MAX        As Integer = 7
' 全局常量――棋子(大)O置x
Public Const PIECES_L_WOOD      As Integer = 0
Public Const PIECES_L_DELICATE  As Integer = 1
Public Const PIECES_L_POLISH    As Integer = 2
Public Const PIECES_L_MAX       As Integer = 3
' 全局常量――音吩O置x
Public Const MUSIC_EXPRESS      As Integer = 0
Public Const MUSIC_FUNNY        As Integer = 1
Public Const MUSIC_CLASSIC      As Integer = 2
Public Const MUSIC_MOZART1      As Integer = 3
Public Const MUSIC_MOZART4      As Integer = 4
Public Const MUSIC_FULELISE     As Integer = 5
Public Const MUSIC_LOVDREAM     As Integer = 6
Public Const MUSIC_WALTZ        As Integer = 7
Public Const MUSIC_HUMOUR       As Integer = 8
Public Const MUSIC_PAL          As Integer = 9
Public Const MUSIC_CMUSIC       As Integer = 10
Public Const MUSIC_NONE         As Integer = 11
Public Const MUSIC_MAX          As Integer = 12
' 全局常量――循h事件
Public Const EVENT_TIMER        As Integer = 0
Public Const EVENT_PLAY         As Integer = 1
Public Const EVENT_ENGINE       As Integer = 2
Public Const EVENT_MUSIC        As Integer = 3
Public Const EVENT_ADVERT       As Integer = 4
Public Const EVENT_GUI          As Integer = 5
' 全局常量――棋P坐
Public Const COORD_CHAR         As String = "１２３４５６７８９九八七六五四三二一"
' 全局常量――子
Public Const HANDICAP_A         As String = "rnbakabnr/9/1c5c1/p1p1p1p1p/9/9/P1P1P1P1P/1C5C1/9/RNBAKAB1R w - - 0 1"
Public Const HANDICAP_B         As String = "rnbakabnr/9/1c5c1/p1p1p1p1p/9/9/P1P1P1P1P/1C5C1/9/R1BAKAB1R w - - 0 1"
Public Const HANDICAP_C         As String = "rnbakabnr/9/1c5c1/p1p1p1p1p/9/9/9/1C5C1/9/RN2K2NR w - - 0 1"
' 全局常量――发布图片大小
Public Const PUB_SIZE_MINI      As Integer = 0
Public Const PUB_SIZE_PRINT     As Integer = 1
Public Const PUB_SIZE_SMALL     As Integer = 2
Public Const PUB_SIZE_LARGE     As Integer = 3
Public Const PUB_SIZE_MAX       As Integer = 4
' 全局常量――发布站点
Public Const PUB_LOCATION_ZGXQDS As Integer = 0
Public Const PUB_LOCATION_TIEBA As Integer = 1
Public Const PUB_LOCATION_ZHIDAO As Integer = 2
Public Const PUB_LOCATION_SINA  As Integer = 3
Public Const PUB_LOCATION_QZONE As Integer = 4
Public Const PUB_LOCATION_OTHER As Integer = 5
Public Const PUB_LOCATION_MAX   As Integer = 6
' 全局常量――发布格式
Public Const PUB_FORMAT_TEXT    As Integer = 0
Public Const PUB_FORMAT_ANSI    As Integer = 1
Public Const PUB_FORMAT_UBB     As Integer = 2
Public Const PUB_FORMAT_HTML    As Integer = 3
Public Const PUB_FORMAT_WEB     As Integer = 4
Public Const PUB_FORMAT_MAX     As Integer = 5

' 全局量――程序
Public App_frmMain              As Form     ' 主窗口ο笠用
Public App_picPieces(0 To 31)   As Picture  ' 棋子D片ο笠用
Public App_picEngineRed         As Picture  ' 引擎碳t的D片ο笠用
Public App_picEngineBlack       As Picture  ' 引擎毯诘D片ο笠用
Public App_lpMainPrev           As Long     ' 橹鞔翱诒Ａ舻氖录触l^程，用在"MainWindowProc"中
Public App_lpThinkPrev          As Long     ' 轱@示思考窗口保留的事件触l^程，用在"ThinkWindowProc"中
Public App_szPath               As String   ' 主程序所在的文件A，以"\"Y尾
Public App_bRunning             As Boolean  ' 程序\行酥荆rO成True，如果是False，t跳出事件zy循h
Public App_szFileName           As String   ' 程序打_的PGN棋V的文件名
Public App_bUpdated             As Boolean  ' 文件已更新的酥荆O成Truet允S⒂谩氨４妗惫δ埽退出r也提示保存
Public App_bAutoEcco            As Boolean  ' 是否⒂米咏馕鲩_局的O置
Public App_bFlipped             As Boolean  ' 是否已翻D棋P
Public App_mvNewMove            As Long     ' 最新的一著法，棋P上有擞
Public App_sqSelected           As Integer  ' 鼠诉x中的棋子
Public App_sqMate               As Integer  ' 被⑺赖()
Public App_bPlay                As Integer  ' 播放棋局
Public App_bRedTurn             As Boolean  ' t黑流酥
Public App_bPromotion           As Boolean  ' 是否允S升
Public App_bPrevInstance        As Boolean  ' 是否再一次
Public App_nEndgames            As Integer  ' x入的局盗
Public App_szEndgames(0 To MAX_ENDGAMES - 1) As String ' x入的局
' 全局量――x
Public Options_bLargeGui        As Boolean  ' 大界面
Public Options_bSounds          As Boolean  ' 音
Public Options_bAutoPlay        As Boolean  ' 自硬シ
Public Options_nPlayIntv        As Integer  ' 播放g隔(秒)
Public Options_bEscapeDouble    As Boolean  ' 用Esc入ANSI
Public Options_bAnsiText        As Boolean  ' 使用ANSI文本棋P
Public Options_bMonoBitmap      As Boolean  ' D片棋P的t子用白底t字表示
Public Options_bPrintDigit      As Boolean  ' 印刷棋P上下@示底
Public Options_nFlashXQ         As Integer  ' Flash棋P尺寸
Public Options_nMoveDelay       As Integer  ' 走子延r
Public Options_bAutoFlip        As Boolean  ' 自臃D棋P
Public Options_bEngineName      As Boolean  ' @示引擎名Q
Public Options_bAllowResign     As Boolean  ' 允S引擎J
Public Options_bAlwaysPonder    As Boolean  ' ⒂冕崤_思考
Public Options_bEngineTime      As Boolean  ' @示引擎速度
Public Options_bStatusBar       As Boolean  ' 出到B
Public Options_nEnginePlay      As Integer  ' 新局引擎B
Public Options_nBoardS          As Integer  ' 小界面的棋P
Public Options_nPiecesS         As Integer  ' 小界面的棋子
Public Options_nBoardL          As Integer  ' 大界面的棋P
Public Options_nPiecesL         As Integer  ' 大界面的棋子
Public Options_nMusic           As Integer  ' 音
Public Options_bTopMost         As Integer  ' 在最前面
Public Options_bPromptSave      As Boolean  ' 提示保存
Public Options_bAutoSave        As Boolean  ' 自颖４
Public Options_szUser           As String   ' 人机弈用
Public Options_bShowNew         As Boolean  ' r@示“_始局”υ框
' 全局量――界面
Public Gui_nLeft                As Long     ' 窗口坐
Public Gui_nTop                 As Long     ' 窗口坐
Public Gui_nEndgamesIndex       As Integer  ' 局的l目序
Public Gui_szEpdName            As String   ' 局文件名
Public Gui_szEpdTitle           As String   ' 局祟}
Public Gui_bShowHowTo           As Boolean  ' r@示“日e月累”
Public Gui_nTipIndex            As Integer  ' “日e月累”的l目序
Public Gui_szTips(1 To TIP_NUM) As String   ' “日e月累”的l目
Public Gui_szQuotes(1 To QUOTE_NUM) As String ' “名言”的l目
Public Gui_szUpgradeVer         As String   ' z查升的版本
Public Gui_szUpgradeDate        As String   ' z查升的日期
Public Gui_szAdvertDate         As String   ' z查V告的日期
Public Gui_nAdvertIndex         As Integer  ' 前V告序
Public Gui_dfLastTime           As Double   ' 前rg(每天00:00^去的秒)
' 全局变量――发布设置
Public Publish_nSize            As Integer  ' 图片类型
Public Publish_bTrad            As Boolean  ' 是否繁体(简单/印刷)
Public Publish_nBoardS          As Integer  ' 棋盘类型(小)
Public Publish_nPiecesS         As Integer  ' 棋子类型(小)
Public Publish_nBoardL          As Integer  ' 棋盘类型(小)
Public Publish_nPiecesL         As Integer  ' 棋子类型(大)
Public Publish_bContent         As Boolean  ' 是否只发布当前局面
Public Publish_nLocation        As Integer  ' 发布站点
Public Publish_nFormat          As Integer  ' 发布格式
' 全局量――棋局
Public Game_szEvent             As String   ' 事
Public Game_szRound             As String   ' 次
Public Game_szDate              As String   ' 日期
Public Game_szSite              As String   ' 地c
Public Game_szRedTeam           As String   ' t方挝
Public Game_szRedPlayer         As String   ' t方x手
Public Game_szBlackTeam         As String   ' 黑方挝
Public Game_szBlackPlayer       As String   ' 黑方x手
Public Game_nResult             As Integer  ' Y果
Public Game_szEcco              As String   ' _局
Public Game_szOpen              As String   ' _局
Public Game_szVar               As String   ' 例
Public Game_bStartPos           As Boolean  ' 是否_始於初始局面
Public Game_cntStart            As Integer  ' 起始回合
Public Game_sdStart             As Integer  ' 起始走子方
Public Game_cntCurr             As Integer  ' 前回合
Public Game_sdCurr              As Integer  ' 前走子方
Public Game_sdMax               As Integer  ' 最後走子方
Public Game_nFormat             As Integer  ' V格式
Public Game_mvMove(1 To 1998)   As Long     ' 著法列表
Public Game_szMoveText(0 To 2, 1 To 1998) As String ' V列表
Public Game_szComment(0 To 1998) As String          ' 注列表
Public Game_szIrrevFen(0 To 40) As String           ' 不可逆局面FEN串列表
Public Game_posIrrev(0 To 40)   As PositionStruct   ' 不可逆局面列表
Public Game_nCurrMove           As Integer  ' 前著法序(第一著法序是1)
Public Game_nMaxMove            As Integer  ' 最大著法序
Public Game_nCurrIrrev          As Integer  ' 前不可逆局面序
Public Game_nMaxIrrev           As Integer  ' 最後不可逆局面序
' 全局量――rg
Public Timer_bEnabled           As Boolean  ' 是否_始r
Public Timer_dfLastTime         As Double   ' 前rg(每天00:00^去的秒)
Public Timer_nDepth             As Integer  ' 引擎搜索深度
Public Timer_bIncMode           As Boolean  ' 是否加r模式
Public Timer_nInitTime          As Integer  ' 起始rg
Public Timer_nIncMove           As Integer  ' r段u的每r段步
Public Timer_nIncTime           As Integer  ' 加ru的加r秒
Public Timer_nTimeLeft(0 To 1)  As Integer  ' p方剩余rg
Public Timer_nTimeElapsed(0 To 1) As Integer ' p方已用去的rg
Public Timer_nMoveLeft(0 To 1)  As Integer  ' 加ru的r段剩余要走的步
Public Timer_bOver(0 To 1)      As Boolean  ' p方是否超r
' 全局量――引擎
Public Engine_bEngineLog        As Boolean  ' 是否引擎日志
Public Engine_szLogFile         As String   ' 引擎日志文件名
Public Engine_bUseBook          As Boolean  ' 是否使用_局
Public Engine_bUseEgtb          As Boolean  ' 是否使用局
Public Engine_szBookFiles       As String   ' _局煳募名
Public Engine_szEgtbPaths       As String   ' 局煳募名
Public Engine_nHash             As Integer  ' 置Q表大小
Public Engine_nThreads          As Integer  ' 引擎使用的程
Public Engine_nRandomness       As Integer  ' S机
Public Engine_nStyle            As Integer  ' L格
Public Engine_szFile            As String   ' 引擎文件
Public Engine_pipe          As PipeStruct   ' 引擎管道
Public Engine_nStatus           As Integer  ' 引擎B
Public Engine_nTimeRatio        As Long     ' 引擎rg比例(秒u是1，毫秒u是1000)
Public Engine_szName            As String   ' 引擎名Q
Public Engine_szVersion         As String   ' 引擎版本
Public Engine_szCopyright       As String   ' 引擎版
Public Engine_szAuthor          As String   ' 引擎作者
Public Engine_szUser            As String   ' 引擎用
Public Engine_posBoard  As PositionStruct   ' 引擎思考的局面
Public Engine_dwTime            As Double   ' 引擎思考的rg
Public Engine_nMoves            As Integer  ' 引擎思考的局面的走法
Public Engine_nDepth            As Integer  ' 引擎思考的深度
Public Engine_nCurrMove         As Integer  ' 引擎思考的局面的前走法
Public Engine_bRed              As Boolean  ' 引擎碳t
Public Engine_bBlack            As Boolean  ' 引擎毯
Public Engine_bPonder           As Boolean  ' 後_思考
Public Engine_bAnalyze          As Boolean  ' 引擎分析
Public Engine_mvPonder          As Long     ' 後_思考猜y著法
Public Engine_mvPonderFinished  As Long     ' 後_思考完成的著法
Public Engine_mvPonderFinishedPonder As Long ' 後_思考完成的猜y著法
Public Engine_bPonderFinishedResign As Long ' 後_思考完成後J
Public Engine_bPonderFinishedDraw As Long   ' 後_思考完成後提和或接受提和
Public Engine_szPonderStr       As String   ' 後_思考指令串
Public Engine_bDraw             As Integer  ' 提和
Public Engine_mvBan             As Long     ' 禁著
Public Engine_bSlaveRed         As Boolean  ' 副引擎碳t
Public Engine_bSlaveBlack       As Boolean  ' 副引擎毯
Public Engine_pipeSlave     As PipeStruct   ' 副引擎管道
Public Engine_nTimeRatioSlave   As Long     ' 副引擎rg比例
Public Engine_szSlaveName       As String   ' 副引擎名Q

' 以下是用函道程

' 把字符DQ成底郑@示棋V坐r用
Public Function Byte2Digit(ByVal szChar As String) As Integer

If szChar >= "1" And szChar <= "9" Then
    Byte2Digit = Asc(szChar) - 48
Else
    Byte2Digit = 0
End If

End Function

' 把底洲DQ成字符，@示棋V坐r用
Public Function Digit2Word(ByVal nArg As Integer) As String

Digit2Word = Choose(nArg, "１", "２", "３", "４", "５", "６", "７", "８", "９")

End Function

' @示著法前的回合担t方格式是“???. 某某某某”，黑方格式是“     某某某某”，著法是R的
Public Function NumberText(cnt As Integer, sd As Integer) As String

NumberText = IIf(sd = 0, LTrim(Str(cnt)) + ".", Space(Len(Str(cnt))))

End Function

' 打_文件υ框
Public Function FileDialog(ByVal szTitle As String, ByVal szFileType As String, ByVal szExt As String, Optional ByVal szSave As String = "") As String

Dim szFileName As String, szFilter As String
szFilter = szFileType + " (*." + szExt + ")|*." + szExt + "|所有文件 (*.*)|*.*"
If szSave = "" Then
    szFileName = OpenFileDialog(szTitle, szFilter)
Else
    szFileName = szSave
    szFileName = Replace(szFileName, "\", "")
    szFileName = Replace(szFileName, "/", "M")
    szFileName = Replace(szFileName, ":", "：")
    szFileName = Replace(szFileName, "*", "＊")
    szFileName = Replace(szFileName, "?", "？")
    ' 全角p引用在某些操作系y上不正常，所以用半角我代替
    szFileName = Replace(szFileName, """", "'")
    szFileName = Replace(szFileName, "<", "＜")
    szFileName = Replace(szFileName, ">", "＞")
    szFileName = Replace(szFileName, "|", "｜")
    szFileName = SaveFileDialog(szTitle, szFilter, szFileName + "." + szExt, szExt)
End If
FileDialog = szFileName

End Function

' 播放音
Public Sub PlayWavSound(ByVal szSoundFile As String)

If Options_bSounds Then
    PlaySoundA App_szPath + "SOUNDS\" + szSoundFile + ".WAV", 0, SND_ASYNC + SND_NOWAIT + SND_FILENAME
End If

End Sub

' 总在最前面
Public Sub SetTopMost(ByVal hWnd As Long)

If Options_bTopMost Then
    SetWindowPos hWnd, HWND_TOPMOST, 0, 0, 0, 0, SWP_NOMOVE + SWP_NOSIZE
End If

End Sub

' 以上是用函道程

' 以下是文件操作例程

' "szSrc"中@得PGN棋V的撕，如果有此撕t返回True，撕热荼４娴"szDst"量中，反之返回False
Public Function GetLabel(ByRef szDst As String, ByVal szSrc As String, ByVal szLabelName As String) As Boolean

Dim nLen As Integer, i As Integer
nLen = Len(szLabelName)
If UCase(Left(szSrc, nLen + 3)) = "[" + szLabelName + " """ Then
    i = InStr(nLen + 4, szSrc, """")
    If i > 0 Then
        szDst = Mid(szSrc, nLen + 4, i - nLen - 4)
    Else
        szDst = Mid(szSrc, nLen + 4)
    End If
    GetLabel = True
Else
    GetLabel = False
End If

End Function

' x入一行PGN文件的热荩返回True表示整棋局Y束，返回False表示後面有热荩x完後剩下的热荼４嬖szLineStr中，以後仍f用"ImportLine"去x
Public Function ImportLine(ByRef szLineStr As String, ByRef szRemStr As String, ByRef nRemLevel As Integer) As Boolean

Dim i As Integer, j As Integer, bEndFor As Boolean
Dim szMoveStr As String, szChar As String, mv As Long

If nRemLevel > 0 Then
    bEndFor = True
    For i = 1 To Len(szLineStr)
        szChar = Mid(szLineStr, i, 1)
        nRemLevel = nRemLevel + Switch(szChar = "(" Or szChar = "{", 1, szChar = ")" Or szChar = "}", -1, True, 0)
        If nRemLevel = 0 Then
            szLineStr = Mid(szLineStr, i + 1)
            Game_szComment(Game_nMaxMove) = Game_szComment(Game_nMaxMove) + szRemStr
            szRemStr = ""
            bEndFor = False
            Exit For
        End If
        szRemStr = szRemStr + szChar
    Next
    If bEndFor Then
        szRemStr = szRemStr + vbCrLf
        szLineStr = ""
    End If
Else
    bEndFor = True
    For i = 1 To Len(szLineStr)
        szChar = Mid(szLineStr, i, 1)
        Select Case szChar
        Case "(", "{"
            nRemLevel = 1
            szLineStr = Mid(szLineStr, i + 1)
            bEndFor = False
            Exit For
        Case "*"
            szLineStr = ""
            ImportLine = True
            Exit Function
        Case "0", "1"
            If Len(szLineStr) - i >= 6 Then
                If Mid(szLineStr, i, 7) = "1/2-1/2" Then
                    Game_nResult = RESULT_DRAW
                    App_frmMain.lblResult = ResultString
                    szLineStr = ""
                    ImportLine = True
                    Exit Function
                End If
            End If
            If Len(szLineStr) - i >= 2 Then
                If Mid(szLineStr, i, 3) = "1-0" Then
                    Game_nResult = RESULT_REDWIN
                    App_frmMain.lblResult = ResultString
                    szLineStr = ""
                    ImportLine = True
                    Exit Function
                ElseIf Mid(szLineStr, i, 3) = "0-1" Then
                    Game_nResult = RESULT_BLACKWIN
                    App_frmMain.lblResult = ResultString
                    szLineStr = ""
                    ImportLine = True
                    Exit Function
                End If
            End If
        Case Else
            If Game_nFormat = 0 Then
                If Asc(szChar) > 255 Or Asc(szChar) < 0 Then
                    j = InStr(i + 1, szLineStr, " ")
                    If j = 0 Then
                        szMoveStr = Mid(szLineStr, i)
                        szLineStr = ""
                    Else
                        szMoveStr = Mid(szLineStr, i, j - i)
                        szLineStr = Mid(szLineStr, j + 1)
                    End If
                    j = Byte2Digit(Mid(szMoveStr, 2, 1))
                    If j > 0 Then
                        szMoveStr = Left(szMoveStr, 1) + Digit2Word(j) + Mid(szMoveStr, 3)
                    End If
                    j = Byte2Digit(Mid(szMoveStr, 4, 1))
                    If j > 0 Then
                        szMoveStr = Left(szMoveStr, 3) + Digit2Word(j)
                    End If
                    mv = CchessFile2Move(CchessChin2File(CvC(szMoveStr)), Game_posIrrev(Game_nCurrIrrev))
                    AddMove mv
                    bEndFor = False
                    Exit For
                End If
            Else
                If (szChar >= "A" And szChar <= "Z") Or (szChar >= "a" And szChar <= "z") Or szChar = "+" Or szChar = "-" Or szChar = "=" Then
                    j = InStr(i + 1, szLineStr, " ")
                    If j = 0 Then
                        szMoveStr = Mid(szLineStr, i)
                        szLineStr = ""
                    Else
                        szMoveStr = Mid(szLineStr, i, j - i)
                        szLineStr = Mid(szLineStr, j + 1)
                    End If
                    If Game_nFormat = 1 Then
                        mv = CchessFile2Move(CvL(szMoveStr), Game_posIrrev(Game_nCurrIrrev))
                    Else
                        mv = Iccs2Move(szMoveStr)
                    End If
                    AddMove mv
                    bEndFor = False
                    Exit For
                End If
            End If
        End Select
    Next
    If bEndFor Then
        szLineStr = ""
    End If
End If
ImportLine = False

End Function

' xPGN棋V，如果是XQF、MXQ、CHE、CHN或CCM棋Vt首先DQPGN棋V
Public Sub ReadFile(Optional ByVal bAutoSave As Boolean = False)

Dim i As Integer, nFileNo As Integer
Dim bNoTitle As Boolean, bCchess As Boolean, bDetail As Boolean
Dim szChineseChess As String, szResult As String, szFormat As String, szFenStr As String
Dim szLineStr As String, szLineStrEx As String, szRemStr As String, nRemLevel As Integer

bNoTitle = False
If UCase(Right(App_szFileName, 4)) = ".XQF" Then
    ConvXqf2Pgn App_szFileName, App_szPath + "AUTOSAVE.PGN"
    bNoTitle = True
ElseIf UCase(Right(App_szFileName, 4)) = ".MXQ" Then
    ConvMxq2Pgn App_szFileName, App_szPath + "AUTOSAVE.PGN"
    bNoTitle = True
ElseIf UCase(Right(App_szFileName, 4)) = ".CHE" Then
    ConvChe2Pgn App_szFileName, App_szPath + "AUTOSAVE.PGN"
    bNoTitle = True
ElseIf UCase(Right(App_szFileName, 4)) = ".CHN" Then
    ConvChn2Pgn App_szFileName, App_szPath + "AUTOSAVE.PGN"
    bNoTitle = True
ElseIf UCase(Right(App_szFileName, 4)) = ".CCM" Then
    ConvCcm2Pgn App_szFileName, App_szPath + "AUTOSAVE.PGN"
    bNoTitle = True
End If
If bAutoSave Or bNoTitle Then
    App_szFileName = ""
    RestartGame
    App_szFileName = App_szPath + "AUTOSAVE.PGN"
Else
    RestartGame
End If

nFileNo = FreeFile
On Error GoTo lnErrorOpen
Open App_szFileName For Input As #nFileNo
On Error GoTo 0
bCchess = False
bDetail = False
szLineStrEx = ""
szLineStr = ""
szRemStr = ""
nRemLevel = 0
Do While Not (EOF(nFileNo) And szLineStr = "" And szLineStrEx = "")
    If szLineStr = "" Then
        If szLineStrEx = "" Then
            Line Input #nFileNo, szLineStrEx
        End If
        i = InStr(szLineStrEx, Chr(10))
        If i > 0 Then
            szLineStr = Left(szLineStrEx, i - 1)
            szLineStrEx = Mid(szLineStrEx, i + 1)
        Else
            szLineStr = szLineStrEx
            szLineStrEx = ""
        End If
    End If
    If bDetail Then
        If Not (bCchess Or App_szFileName = "") Then
            MsgBox App_szFileName + " 可能不是中象棋的PGN文件。", vbExclamation
            App_szFileName = ""
            SetTitle
        End If
        If ImportLine(szLineStr, szRemStr, nRemLevel) Then
            Exit Do
        End If
    Else
        szLineStr = LTrim(szLineStr)
        If szLineStr = "" Then
        ElseIf Left(szLineStr, 1) = "[" Then
            If GetLabel(szChineseChess, szLineStr, "GAME") Then
                If UCase(szChineseChess) = "CHINESE CHESS" Then
                    bCchess = True
                End If
            ElseIf GetLabel(Game_szEvent, szLineStr, "EVENT") Then
            ElseIf GetLabel(Game_szRound, szLineStr, "ROUND") Then
            ElseIf GetLabel(Game_szDate, szLineStr, "DATE") Then
            ElseIf GetLabel(Game_szSite, szLineStr, "SITE") Then
            ElseIf GetLabel(Game_szRedTeam, szLineStr, "REDTEAM") Then
            ElseIf GetLabel(Game_szRedPlayer, szLineStr, "RED") Then
            ElseIf GetLabel(Game_szBlackTeam, szLineStr, "BLACKTEAM") Then
            ElseIf GetLabel(Game_szBlackPlayer, szLineStr, "BLACK") Then
            ElseIf GetLabel(szResult, szLineStr, "RESULT") Then
                Game_nResult = Switch(szResult = "1-0", RESULT_REDWIN, szResult = "1/2-1/2", RESULT_DRAW, szResult = "0-1", RESULT_BLACKWIN, True, RESULT_UNKNOWN)
            ElseIf GetLabel(Game_szEcco, szLineStr, "ECCO") Then
                App_bAutoEcco = False
            ElseIf GetLabel(Game_szOpen, szLineStr, "OPENING") Then
                App_bAutoEcco = False
            ElseIf GetLabel(Game_szVar, szLineStr, "VARIATION") Then
                App_bAutoEcco = False
            ElseIf GetLabel(szFormat, szLineStr, "FORMAT") Then
                Game_nFormat = Switch(UCase(szFormat) = "WXF", 1, UCase(szFormat) = "ICCS", 2, True, 0)
            ElseIf GetLabel(szFenStr, szLineStr, "FEN") Then
                Game_szIrrevFen(0) = szFenStr
                CchessFen2Board Game_posIrrev(0), szFenStr
                Game_sdCurr = Game_posIrrev(0).sdPlayer
                i = InStr(szFenStr, " - - 0 ")
                If i > 0 Then
                    Game_cntCurr = Str2Int(Mid(szFenStr, i + 7), 1, 999)
                Else
                    Game_cntCurr = 1
                End If
                Game_sdStart = Game_sdCurr
                Game_cntStart = Game_cntCurr
                Game_sdMax = Game_sdStart
                Game_bStartPos = False
            End If
            szLineStr = ""
        Else
            bDetail = True
        End If
    End If
Loop
Close #nFileNo
BoardFlush
LabelFlush
If bAutoSave Then
    If Game_bStartPos And Game_nMaxMove = 0 Then
        EngineNewGame
    End If
Else
    Do While Game_nCurrMove > 0
        MoveBack
    Loop
End If
MoveFlush
If bAutoSave Or bNoTitle Then
    App_szFileName = ""
End If

Exit Sub
lnErrorOpen:
On Error GoTo 0
If Not bAutoSave Then
    MsgBox "o法打_文件 " + App_szFileName, vbExclamation
End If
BoardFlush
LabelFlush
MoveFlush
If bAutoSave Then
    EngineNewGame
End If
If bAutoSave Or bNoTitle Then
    App_szFileName = ""
End If

End Sub

' 存撕
Public Function PutLabel(ByVal LabelName As String, ByVal LabelValue As String) As String

LabelValue = Replace(LabelValue, Chr(13), "")
LabelValue = Replace(LabelValue, Chr(10), "")
PutLabel = "[" + LabelName + " """ + LabelValue + """]"

End Function

' PGN棋V
Public Sub WriteFile(Optional ByVal bTempFile As Boolean = False)

Dim sd As Integer, cnt As Integer
Dim i As Integer, bReturned As Boolean, nFileNo As Integer
nFileNo = FreeFile
On Error GoTo lnErrorOpen
Open IIf(bTempFile, App_szPath + TEMP_FILE_NAME, App_szFileName) For Output As #nFileNo
On Error GoTo 0
Print #nFileNo, PutLabel("Game", "Chinese Chess")
Print #nFileNo, PutLabel("Event", Game_szEvent)
Print #nFileNo, PutLabel("Round", Game_szRound)
Print #nFileNo, PutLabel("Date", Game_szDate)
Print #nFileNo, PutLabel("Site", Game_szSite)
Print #nFileNo, PutLabel("RedTeam", Game_szRedTeam)
Print #nFileNo, PutLabel("Red", Game_szRedPlayer)
Print #nFileNo, PutLabel("BlackTeam", Game_szBlackTeam)
Print #nFileNo, PutLabel("Black", Game_szBlackPlayer)
Print #nFileNo, PutLabel("Result", Choose(Game_nResult + 1, "*", "1-0", "1/2-1/2", "0-1"))
Print #nFileNo, PutLabel("ECCO", Game_szEcco)
Print #nFileNo, PutLabel("Opening", Game_szOpen)
Print #nFileNo, PutLabel("Variation", Game_szVar)
If Not Game_bStartPos Then
    Print #nFileNo, PutLabel("FEN", Game_szIrrevFen(0))
End If
If Game_nFormat > 0 Then
    Print #nFileNo, PutLabel("Format", IIf(Game_nFormat = 1, "WXF", "ICCS"))
End If
If Game_szComment(0) <> "" Then
    Print #nFileNo, "{" + Game_szComment(0) + "}"
End If
bReturned = True
cnt = Game_cntStart
sd = Game_sdStart
For i = 1 To Game_nMaxMove
    If bReturned Then
        Print #nFileNo, Space(4 - Len(Str(cnt))) + LTrim(Str(cnt)) + ". ";
        If sd = 1 Then
            Print #nFileNo, Space(Choose(Game_nFormat + 1, 8, 4, 5));
        End If
    End If
    If sd = 0 Then
        Print #nFileNo, Game_szMoveText(Game_nFormat, i);
        bReturned = False
    Else
        Print #nFileNo, " " + Game_szMoveText(Game_nFormat, i)
        bReturned = True
    End If
    If Game_szComment(i) <> "" Then
        If Not bReturned Then
            Print #nFileNo,
        End If
        Print #nFileNo, "{" + Game_szComment(i) + "}"
        bReturned = True
    End If
    sd = 1 - sd
    cnt = cnt + 1 - sd
Next
Print #nFileNo, Choose(Game_nResult + 1, " *", " 1-0", " 1/2-1/2", " 0-1")
Print #nFileNo, "============================"
Print #nFileNo, " g迎L《象棋百科全W》 "
Print #nFileNo, " 推荐用《象棋巫》^p棋V "
Print #nFileNo, "http://www.elephantbase.net/"
Close #nFileNo
If Not bTempFile Then
    App_bUpdated = False
    App_frmMain.mnFileSave.Enabled = False
    App_frmMain.tlb.Buttons("FileSave").Enabled = False
End If

Exit Sub
lnErrorOpen:
On Error GoTo 0
MsgBox "o法打_文件 " + IIf(bTempFile, App_szPath + TEMP_FILE_NAME, App_szFileName), vbExclamation

End Sub

' 如果棋局改舆^，就要O置“已更新”B，保存功能有效
Public Sub FileUpdated()

App_bUpdated = True
App_frmMain.mnFileSave.Enabled = True
App_frmMain.tlb.Buttons("FileSave").Enabled = True

End Sub

' 打_棋V
Public Sub OpenFile()

Dim szFileName As String
szFileName = OpenFileDialog("打_棋V文件", "所有棋V (*.PGN;*.XQF;*.MXQ;*.CHE;*.CHN;*.CCM)|*.PGN;*.XQF;*.MXQ;*.CHE;*.CHN;*.CCM|" + _
        "可移植棋V (*.PGN)|*.PGN|象棋演播室棋V (*.XQF)|*.XQF|弈天棋V (*.MXQ)|*.MXQ|QQ象棋棋V (*.CHE)|*.CHE|象棋棋V (*.CHN)|*.CHN|中游象棋棋V (*.CCM)|*.CCM|所有文件 (*.*)|*.*")
If szFileName <> "" Then
    App_szFileName = szFileName
    ReadFile
End If

End Sub

' 如果是“已更新”B，那么提示保存PGN棋V
Public Function CancelSave() As Boolean

If Not (Options_bPromptSave And App_bUpdated) Then
    CancelSave = False
    Exit Function
End If
Select Case MsgBox(IIf(App_szFileName = "", "文件已更改。", "文件 " + App_szFileName + " 已更改。") + vbCrLf + "是否保存更改？", vbYesNoCancel + vbExclamation)
Case vbYes
    If App_szFileName = "" Then
        If SaveFileAs() Then
            CancelSave = False
        Else
            CancelSave = True
        End If
    Else
        WriteFile
        CancelSave = False
    End If
Case vbNo
    CancelSave = False
Case Else
    CancelSave = True
End Select

End Function

' F“另存椤惫δ
Public Function SaveFileAs()

Dim sz As String
sz = FileDialog("保存棋V文件", "可移植棋V", "PGN", DefaultFileName)
If sz <> "" Then
    App_szFileName = sz
    SetTitle
    WriteFile
    SaveFileAs = True
Else
    SaveFileAs = False
End If

End Function

' F“出XQF格式”功能
Public Sub ExportXqf()

Dim sz As String
sz = FileDialog("出XQF格式", "象棋演播室棋V", "XQF", DefaultFileName)
If sz <> "" Then
    WriteFile WRITE_TEMP_FILE
    ConvPgn2Xqf App_szPath + TEMP_FILE_NAME, sz
End If

End Sub

' 打_FEN局面
Public Sub LoadFenFile(ByVal szFenFile As String)

Dim nFileNo As Integer, szFenStr As String
nFileNo = FreeFile
On Error GoTo lnErrorOpen
Open szFenFile For Input As #nFileNo
On Error GoTo 0
If EOF(nFileNo) Then
    szFenStr = ""
Else
    Line Input #nFileNo, szFenStr
End If
Close #nFileNo
LoadFenStr szFenStr

Exit Sub
lnErrorOpen:
On Error GoTo 0
MsgBox "o法打_文件 " + szFenFile, vbExclamation

End Sub

' b入FEN字符串
Public Sub LoadFenStr(ByVal szFenStr As String)

Dim i As Integer
If szFenStr = "" Then
    Exit Sub
End If
szFenStr = Replace(szFenStr, Chr(13), "")
szFenStr = Replace(szFenStr, Chr(10), "")
App_szFileName = ""
RestartGame
Game_bStartPos = False
Game_szIrrevFen(0) = szFenStr
CchessFen2Board Game_posIrrev(0), szFenStr
Game_sdCurr = Game_posIrrev(0).sdPlayer
i = InStr(szFenStr, " - - 0 ")
If i > 0 Then
    Game_cntCurr = Str2Int(Mid(szFenStr, i + 7), 1, 999)
Else
    Game_cntCurr = 1
End If
Game_sdStart = Game_sdCurr
Game_cntStart = Game_cntCurr
Game_sdMax = Game_sdStart
BoardFlush
LabelFlush
MoveFlush

End Sub

' 以上是文件操作例程

' 以下是界面控u例程

' 出rg格式："hh:mm:ss"
Public Function TimeText(ByVal nSeconds As Long) As String

Dim sz As String
sz = Chr((nSeconds \ 36000) Mod 6 + 48) + Chr((nSeconds \ 3600) Mod 10 + 48) + ":"
sz = sz + Chr((nSeconds \ 600) Mod 6 + 48) + Chr((nSeconds \ 60) Mod 10 + 48) + ":"
sz = sz + Chr((nSeconds \ 10) Mod 6 + 48) + Chr((nSeconds \ 1) Mod 10 + 48)
TimeText = sz

End Function

' @示rg，"0"=t方，"1"=黑方，"-1"=走棋方
Public Sub SetTimer(Optional ByVal sd As Integer = -1)

Dim nSeconds As Integer, sdThis As Integer
sdThis = IIf(sd = -1, Game_sdMax, sd)
nSeconds = IIf(sd = -1, Game_sdMax, sd)
If sdThis = 0 Eqv App_bFlipped Then
    App_frmMain.lblTimeLeftUp.Caption = TimeText(Timer_nTimeLeft(sdThis))
    App_frmMain.lblTimeRuleUp.Caption = IIf(Timer_bIncMode, "每步加" + Str(Timer_nIncTime) + " 秒", "走完" + Str(Timer_nMoveLeft(sdThis)) + " 步")
    App_frmMain.lblTimeElapsedUp.Caption = TimeText(Timer_nTimeElapsed(sdThis))
Else
    App_frmMain.lblTimeLeftDown.Caption = TimeText(Timer_nTimeLeft(sdThis))
    App_frmMain.lblTimeRuleDown.Caption = IIf(Timer_bIncMode, "每步加" + Str(Timer_nIncTime) + " 秒", "走完" + Str(Timer_nMoveLeft(sdThis)) + " 步")
    App_frmMain.lblTimeElapsedDown.Caption = TimeText(Timer_nTimeElapsed(sdThis))
End If

End Sub

' 事字符串
Public Function EventString() As String
    
Dim sz As String
sz = Game_szEvent
If Game_szRound <> "" Then
    sz = sz + IIf(Game_szEvent = "", "", " ")
    If Len(Game_szRound) > 5 Then
        sz = sz + Game_szRound
    Else
        Select Case Str2Int(Game_szRound)
        Case 0
            sz = sz + Game_szRound
        Case 1 To 97
            sz = sz + "第" + Game_szRound + ""
        Case 98
            sz = sz + "半Q"
        Case 99
            sz = sz + "Q"
        End Select
    End If
End If
EventString = sz

End Function

' 比Y果字符串
Public Function ResultString() As String

If Game_szRedPlayer = "" Or Game_szBlackPlayer = "" Then
    ResultString = "(著法：" + IIf(Game_sdStart = 0, "t先", "黑先") + Choose(Game_nResult + 1, "", IIf(Game_sdStart = 0, "胜", ""), "和", IIf(Game_sdStart = 0, "", "胜")) + ")"
Else
    ResultString = Game_szRedTeam + IIf(Game_szRedTeam = "", "", " ") + Game_szRedPlayer + _
            Choose(Game_nResult + 1, " () ", IIf(Game_sdStart = 0, " (先胜) ", " (後胜) "), IIf(Game_sdStart = 0, " (先和) ", " (後和) "), IIf(Game_sdStart = 0, " (先) ", " (後) ")) + _
            Game_szBlackTeam + IIf(Game_szBlackTeam = "", "", " ") + Game_szBlackPlayer
End If

End Function

' 日期和地c字符串
Public Function DateSiteString() As String

If Game_szDate = "" Then
    DateSiteString = IIf(Game_szSite = "", "", "(弈於 " + Game_szSite + ")")
Else
    DateSiteString = "(" + Game_szDate + IIf(Game_szSite = "", ")", " 弈於 " + Game_szSite + ")")
End If

End Function

' _局字符串
Public Function OpenString() As String

OpenString = Game_szOpen + IIf(Game_szOpen = "" Or Game_szVar = "", "", "――") + Game_szVar + _
        Switch(Game_szEcco = "", "", Game_szOpen = "" And Game_szVar = "", "(ECCO_局：" + Game_szEcco + ")", True, "(" + Game_szEcco + ")")

End Function

' 缺省文件名
Public Function DefaultFileName() As String

Dim sz As String
sz = EventString
DefaultFileName = IIf(sz = "", "", sz + " - ") + ResultString

End Function

' @示全部撕
Public Sub LabelFlush()

App_frmMain.imgUp.Picture = IIf(App_bFlipped, IIf(Engine_bRed, App_picEngineRed, App_picPieces(1)), IIf(Engine_bBlack, App_picEngineBlack, App_picPieces(8)))
App_frmMain.imgDown.Picture = IIf(App_bFlipped, IIf(Engine_bBlack, App_picEngineBlack, App_picPieces(8)), IIf(Engine_bRed, App_picEngineRed, App_picPieces(1)))
App_frmMain.cmbFormat.ListIndex = Game_nFormat
App_frmMain.lblEvent = EventString
App_frmMain.lblResult = ResultString
App_frmMain.lblDateSite = DateSiteString
App_frmMain.lblOpen = OpenString
SetTimer 0
SetTimer 1

End Sub

' 清空棋P
Public Sub NewGame0()

RestartGame
BoardFlush
LabelFlush
MoveFlush

End Sub

' “新的局”功能
Public Sub NewGame()

If Options_bShowNew Then
    frmNewGame.Show vbModal, App_frmMain
    Exit Sub
End If
App_szFileName = ""
NewGame0
EngineNewGame

End Sub

' Y束一局局
Public Sub EndGame(ByVal szSound As String, ByVal szPrompt As String)

Dim mbp As MsgBoxParams, dwResult As Long

PlayWavSound szSound
If App_sqMate > 0 Then
    DrawSquare App_sqMate, , DRAW_MATE
End If
mbp.cbSize = Len(mbp)
mbp.hwndOwner = App_frmMain.hWnd
mbp.hInstance = 0
mbp.lpszText = szPrompt + vbCrLf + vbCrLf + "再硪痪幔"
mbp.lpszCaption = App.Title
mbp.dwStyle = MB_USERICON + MB_YESNO
mbp.lpszIcon = IDI_QUESTION
mbp.dwContextHelpId = 0
mbp.lpfnMsgBoxCallback = 0
mbp.dwLanguageId = 0
' Unsupported under Windows 98
dwResult = MessageBoxIndirectA(mbp)
If dwResult = 0 Then
    mbp.hInstance = App.hInstance
    mbp.lpszIcon = 1
    dwResult = MessageBoxIndirectA(mbp)
End If

If dwResult = vbYes Then
    If Not CancelSave Then
        NewGame
    End If
End If

End Sub

' 新_一局，在新建文件、棋局等功能中使用，棋局要加"RESTART_KEEP_LABEL"，不清除撕
Public Sub RestartGame(Optional ByVal bKeepLabel As Boolean = False, Optional ByVal bDebugUcci As Boolean = False)

If Engine_nStatus > IDLE_UNLOAD Then
    If Engine_nStatus > BUSY_WAIT Then
        Engine_nStatus = BUSY_WAIT
        StopEngine
    End If
    Engine_nStatus = IDLE_READY
    If Not bDebugUcci Then
        SendEngine "setoption newgame"
        If Engine_bSlaveRed Or Engine_bSlaveBlack Then
            SendEngine "setoption newgame", SLAVE_ENGINE
            SendEngine "setoption ponder false", SLAVE_ENGINE
            SendEngine "setoption ponder false"
        Else
            SendEngine "setoption ponder " + IIf(Options_bAlwaysPonder, "true", "false")
        End If
    End If
    EngineLoaded True
End If
Game_szIrrevFen(0) = CCHESS_START_FEN + " - - 0 1"
CchessFen2Board Game_posIrrev(0), Game_szIrrevFen(0)
SetTitle
App_frmMain.mnFileSave.Enabled = False
App_frmMain.tlb.Buttons("FileSave").Enabled = False
If Not bKeepLabel Then
    Game_szEvent = ""
    Game_szRound = ""
    Game_szDate = ""
    Game_szSite = ""
    Game_szRedTeam = ""
    Game_szRedPlayer = ""
    Game_szBlackTeam = ""
    Game_szBlackPlayer = ""
    Game_nResult = RESULT_UNKNOWN
    Game_nFormat = 0
    Game_szEcco = ""
    Game_szOpen = ""
    Game_szVar = ""
    Game_sdStart = 0
    Game_cntStart = 1
    Game_sdCurr = 0
    Game_cntCurr = 1
    App_frmMain.cmbFormat.ListIndex = 0
    App_bAutoEcco = True
End If
Game_nCurrMove = 0
Game_nCurrIrrev = 0
Game_bStartPos = True
MoveCut
Game_szComment(0) = ""
App_bUpdated = False
App_bFlipped = False
CoordFlush
App_mvNewMove = 0
App_frmMain.mnPosFlip.Checked = False
App_frmMain.tlb.Buttons("PosFlip").Value = tbrUnpressed
App_bPlay = Options_bAutoPlay
App_frmMain.mnWizardPlay.Checked = App_bPlay
App_frmMain.tlb.Buttons("WizardPlay").Value = IIf(App_bPlay, tbrPressed, tbrUnpressed)
Timer_bOver(0) = False
Timer_bOver(1) = False
Timer_nTimeLeft(0) = Timer_nInitTime * 60
Timer_nTimeLeft(1) = Timer_nInitTime * 60
Timer_nTimeElapsed(0) = 0
Timer_nTimeElapsed(1) = 0
Timer_nMoveLeft(0) = Timer_nIncMove
Timer_nMoveLeft(1) = Timer_nIncMove
Timer_dfLastTime = Timer
Timer_bEnabled = False
App_frmMain.tlb.Buttons("EngineLevel").Value = tbrUnpressed

End Sub

' 在棋P上@示棋子，如果指定"DRAW_SELECTED"担t再@示一擞框，"frmPosEdit"中有相的"DrawPosSquare/DrawCapSquare"^程
Public Sub DrawSquare(ByVal sq As Integer, Optional ByVal bSelected As Boolean = False, Optional ByVal bMate As Boolean = False)

Dim pc As Integer, nPicId As Integer, img As Image, i As Integer, x As Integer, y As Integer
pc = Game_posIrrev(Game_nCurrIrrev).ucpcSquares(sq)
If pc = 0 Then
    nPicId = 0
Else
    nPicId = PieceType(pc) + IIf(pc < 32, 1, 8)
End If
nPicId = nPicId + IIf(bSelected, 15, 0)
Set img = App_frmMain.imgSquares(IIf(App_bFlipped, (12 - sq \ 16) * 9 + 11 - sq Mod 16, (sq \ 16 - 3) * 9 + sq Mod 16 - 3))
If bMate Then
    nPicId = IIf(pc = 16, 30, IIf(pc = 32, 31, nPicId))
    ' 以下是新功能 - 抖一下⑺赖淖
    img.ZOrder
    x = img.Left
    y = img.Top
    For i = 5 To 1 Step -1
        img.Move x + i * 20, y
        img.Refresh
        Sleep 50
        img.Move x - i * 20, y
        img.Refresh
        Sleep 50
    Next
    img.Move x, y
    ' 以上是新功能 - 抖一下⑺赖淖
End If
img.Picture = App_picPieces(nPicId)

End Sub

' @示整棋P，"frmPosEdit"中有一相的"PosBoardFlush"^程
Public Sub BoardFlush()

Dim i As Integer, j As Integer
App_sqSelected = 0
App_sqMate = 0
For i = 3 To 12
    For j = 3 To 11
        DrawSquare i * 16 + j
    Next
Next

End Sub

' @示坐
Public Sub CoordFlush()

Dim i As Integer
If App_bFlipped Then
    For i = 0 To 17
        App_frmMain.btnCoord(i).Caption = Mid(COORD_CHAR, 18 - i, 1)
    Next
Else
    For i = 0 To 17
        App_frmMain.btnCoord(i).Caption = Mid(COORD_CHAR, i + 1, 1)
    Next
End If

End Sub

' @示前走^的著法，并且根据哪方走棋更新主窗口的D
Public Sub MoveFlush()

Dim bNotBegin As Boolean, bNotEnd As Boolean
If App_sqSelected > 0 Then
    DrawSquare App_sqSelected
    App_sqSelected = 0
End If
If App_sqMate > 0 Then
    DrawSquare App_sqMate
    App_sqMate = 0
End If
If App_mvNewMove <> 0 Then
    DrawSquare Src(App_mvNewMove)
    DrawSquare Dst(App_mvNewMove)
    App_mvNewMove = 0
End If
If Game_nCurrMove > 0 Then
    App_mvNewMove = Game_mvMove(Game_nCurrMove)
    DrawSquare Src(App_mvNewMove), DRAW_SELECTED
    DrawSquare Dst(App_mvNewMove), DRAW_SELECTED
End If
bNotBegin = (Game_nCurrMove > 0)
App_frmMain.mnMoveDel.Enabled = bNotBegin
App_frmMain.tlb.Buttons("MoveDel").Enabled = bNotBegin
App_frmMain.mnMoveStart.Enabled = bNotBegin
App_frmMain.tlb.Buttons("MoveStart").Enabled = bNotBegin
App_frmMain.mnMoveBack.Enabled = bNotBegin
App_frmMain.tlb.Buttons("MoveBack").Enabled = bNotBegin
bNotEnd = (Game_nCurrMove < Game_nMaxMove)
App_frmMain.mnMoveForward.Enabled = bNotEnd
App_frmMain.tlb.Buttons("MoveForward").Enabled = bNotEnd
App_frmMain.mnMoveEnd.Enabled = bNotEnd
App_frmMain.tlb.Buttons("MoveEnd").Enabled = bNotEnd
App_frmMain.mnEngineRetract.Enabled = bNotBegin And Not bNotEnd
App_frmMain.mnEngineBan.Enabled = bNotBegin And Not bNotEnd
If Not bNotEnd Then
    App_bPlay = False
    App_frmMain.mnWizardPlay.Checked = False
    App_frmMain.tlb.Buttons("WizardPlay").Value = tbrUnpressed
End If
App_frmMain.txtComment.Text = Game_szComment(Game_nCurrMove)
App_frmMain.Icon = IIf(Game_sdMax = 0, frmHide.imgFormRed.Picture, frmHide.imgFormBlack.Picture)
If Engine_bAnalyze And Not (Engine_bRed Or Engine_bBlack) Then
    If Engine_nStatus > BUSY_WAIT Then
        Engine_nStatus = BUSY_WAIT
        StopEngine
    End If
    Engine_nStatus = IDLE_READY
End If
Engine_bDraw = False
Engine_mvBan = 0
App_frmMain.lstMoveText.ListIndex = Game_nCurrMove
App_frmMain.mnMovePromote.Enabled = False
App_frmMain.tlb.Buttons("MovePromote").Enabled = False

End Sub

' 後退一著法
Public Sub MoveBack()

Dim i As Integer, Status As Long, mv As Long
If Game_posIrrev(Game_nCurrIrrev).nMoveNum = 1 Then
    Game_nCurrIrrev = Game_nCurrIrrev - 1
Else
    CchessUndoMove Game_posIrrev(Game_nCurrIrrev)
End If
DrawSquare Src(Game_mvMove(Game_nCurrMove))
DrawSquare Dst(Game_mvMove(Game_nCurrMove))
Game_nCurrMove = Game_nCurrMove - 1
Game_sdCurr = 1 - Game_sdCurr
Game_cntCurr = Game_cntCurr - Game_sdCurr

End Sub

' 前M一著法
Public Sub MoveForward()

Dim nMoveStatus As Long
Game_nCurrMove = Game_nCurrMove + 1
CchessTryMove Game_posIrrev(Game_nCurrIrrev), nMoveStatus, Game_mvMove(Game_nCurrMove)
If (nMoveStatus And MOVE_CAPTURE) <> 0 Then
    CchessUndoMove Game_posIrrev(Game_nCurrIrrev)
    Game_nCurrIrrev = Game_nCurrIrrev + 1
End If
DrawSquare Dst(Game_mvMove(Game_nCurrMove))
DrawSquare Src(Game_mvMove(Game_nCurrMove))
Game_sdCurr = 1 - Game_sdCurr
Game_cntCurr = Game_cntCurr + 1 - Game_sdCurr

End Sub

' 清除後面所有的著法
Public Sub MoveCut()

Dim i As Integer
If Engine_nStatus > BUSY_WAIT Then
    Engine_nStatus = BUSY_WAIT
    StopEngine
End If
For i = Game_nMaxMove To Game_nCurrMove + 1 Step -1
    App_frmMain.lstMoveText.RemoveItem i
    Game_szComment(i) = ""
Next
Game_sdMax = Game_sdCurr
Game_nMaxMove = Game_nCurrMove
Game_nMaxIrrev = Game_nCurrIrrev

End Sub

' 新增一著法，可以是奈募中x入的，用舭岬模或引擎走的
Public Function AddMove(mv As Long, Optional ByVal bEcho = False, Optional ByVal bEngine = False) As Boolean

Dim i As Integer, nMoveStatus As Long, szResult As String
Dim szMoveChinText As String, szMoveFileText As String, szMoveIccsText As String

If Game_cntCurr = 1000 Or Game_posIrrev(Game_nCurrIrrev).nMoveNum = 256 Then
    AddMove = False
    If bEcho Then
        MsgBox "超^最大回合迪扪u！", vbExclamation
    End If
    Exit Function
End If
szMoveIccsText = Move2Iccs(mv)
szMoveFileText = MkL(CchessMove2File(mv, Game_posIrrev(Game_nCurrIrrev)))
szMoveChinText = MkC(CchessFile2Chin(CvL(szMoveFileText), Game_sdCurr))
If CchessTryMove(Game_posIrrev(Game_nCurrIrrev), nMoveStatus, mv) Then
    CchessUndoMove Game_posIrrev(Game_nCurrIrrev)
    If Engine_nStatus > BUSY_WAIT Then
        If Engine_nStatus = BUSY_PONDER And mv = Engine_mvPonder Then
            SendEngine "ponderhit", IIf(Engine_posBoard.sdPlayer = 0, Engine_bSlaveRed, Engine_bSlaveBlack)
            Engine_nStatus = BUSY_THINK
        Else
            Engine_nStatus = BUSY_WAIT
            StopEngine
        End If
    End If
    If Game_nCurrMove < Game_nMaxMove Then
        If MsgBox("必清除以後所有的著法。" + vbCrLf + "是否^m？", vbYesNo + vbQuestion) = vbYes Then
            MoveCut
        Else
            AddMove = False
            Exit Function
        End If
    End If
    If (nMoveStatus And MOVE_CAPTURE) <> 0 Then
        Game_posIrrev(Game_nMaxIrrev + 1) = Game_posIrrev(Game_nMaxIrrev)
        Game_nMaxIrrev = Game_nMaxIrrev + 1
        CchessTryMove Game_posIrrev(Game_nMaxIrrev), nMoveStatus, mv
        CchessSetIrrev Game_posIrrev(Game_nMaxIrrev)
        Game_szIrrevFen(Game_nMaxIrrev) = AllocString(CchessBoard2Fen(Game_posIrrev(Game_nMaxIrrev))) + " - - 0" + _
                Str(IIf(Game_sdCurr = 0, Game_cntCurr, Game_cntCurr + 1))
    End If
    If Timer_bEnabled And Not Timer_bOver(Game_sdMax) Then
        If Timer_bIncMode Then
            Timer_nTimeLeft(Game_sdMax) = Timer_nTimeLeft(Game_sdMax) + Timer_nIncTime
            If Timer_nTimeLeft(Game_sdMax) < 0 Then
                Timer_nTimeLeft(Game_sdMax) = 0
            ElseIf Timer_nTimeLeft(Game_sdMax) > 28800 Then
                Timer_nTimeLeft(Game_sdMax) = 28800
            End If
            SetTimer
        Else
            Timer_nMoveLeft(Game_sdMax) = Timer_nMoveLeft(Game_sdMax) - 1
            If Timer_nMoveLeft(Game_sdMax) = 0 Then
                Timer_nTimeLeft(Game_sdMax) = Timer_nInitTime * 60
                Timer_nMoveLeft(Game_sdMax) = Timer_nIncMove
                SetTimer
            End If
        End If
    End If
    Timer_dfLastTime = Timer
    Game_nMaxMove = Game_nMaxMove + 1
    Game_sdMax = 1 - Game_sdMax
    Game_szMoveText(0, Game_nMaxMove) = szMoveChinText
    Game_szMoveText(1, Game_nMaxMove) = szMoveFileText
    Game_szMoveText(2, Game_nMaxMove) = szMoveIccsText
    App_frmMain.lstMoveText.AddItem NumberText(Game_cntCurr, Game_sdCurr) + Game_szMoveText(Game_nFormat, Game_nMaxMove), Game_nMaxMove
    Game_mvMove(Game_nMaxMove) = mv
    MoveForward
    If bEcho Then
        MoveFlush
    End If
    AnalyzeEcco
    If (nMoveStatus And MOVE_MATE) <> 0 Then
        App_sqMate = Game_posIrrev(Game_nCurrIrrev).ucsqPieces(IIf(Game_sdCurr = 1, 32, 16))
        If bEcho Then
            szResult = IIf((Game_sdCurr = 1 And Engine_bRed) Or (Game_sdCurr = 0 And Engine_bBlack), "LOSS", "WIN")
            EndGame szResult, IIf(Game_sdCurr = 1, "t胜。", "黑胜。")
        End If
        Game_nResult = IIf(Game_sdCurr = 1, RESULT_REDWIN, RESULT_BLACKWIN)
        App_frmMain.lblResult = ResultString
        Timer_bEnabled = False
        App_frmMain.mnEngineLevel.Checked = False
        App_frmMain.tlb.Buttons("EngineLevel").Value = tbrUnpressed
    Else
        If bEcho Then
            If (nMoveStatus And MOVE_CHECK) <> 0 Then
                PlayWavSound "CHECK" + IIf(bEngine, "2", "")
            ElseIf (nMoveStatus And MOVE_CAPTURE) <> 0 Then
                PlayWavSound IIf(Src(mv) = Dst(mv), "PROMOTE", "CAPTURE") + IIf(bEngine, "2", "")
            Else
                PlayWavSound "MOVE" + IIf(bEngine, "2", "")
            End If
        End If
        If (nMoveStatus And MOVE_CONSECUTIVE) <> 0 Then
            If bEcho Then
                EndGame "DRAW", "自然作和。"
            End If
            Game_nResult = RESULT_DRAW
            App_frmMain.lblResult = ResultString
            Timer_bEnabled = False
            App_frmMain.mnEngineLevel.Checked = False
            App_frmMain.tlb.Buttons("EngineLevel").Value = tbrUnpressed
            If Engine_bRed Or Engine_bBlack Then
                EngineLoaded True
            End If
        ElseIf (nMoveStatus And MOVE_PERPETUAL) <> 0 Then
            Game_nResult = RESULT_DRAW
            If (nMoveStatus And MOVE_PERPETUAL_WIN) <> 0 Then
                Game_nResult = Game_nResult + 1 - Game_sdCurr * 2
            End If
            If (nMoveStatus And MOVE_PERPETUAL_LOSS) <> 0 Then
                Game_nResult = Game_nResult - 1 + Game_sdCurr * 2
            End If
            If bEcho Then
                If Game_nResult = 2 Then
                    szResult = "DRAW"
                Else
                    szResult = IIf((Game_nResult = 1 And Engine_bRed) Or (Game_nResult = 3 And Engine_bBlack), "LOSS", "WIN")
                End If
                EndGame szResult, Choose(Game_nResult, "黑方L打作！", "p方不作和。", "t方L打作！")
            End If
            App_frmMain.lblResult = ResultString
            Timer_bEnabled = False
            App_frmMain.mnEngineLevel.Checked = False
            App_frmMain.tlb.Buttons("EngineLevel").Value = tbrUnpressed
            If Engine_bRed Or Engine_bBlack Then
                EngineLoaded True
            End If
        End If
    End If
    If App_frmMain.mnEngineDraw.Checked Then
        App_frmMain.mnEngineDraw.Checked = False
        Engine_bDraw = True
    End If
    If Engine_nStatus = IDLE_PONDER Then
        If mv = Engine_mvPonder Then
            If Options_bAllowResign And Engine_bPonderFinishedResign Then
                Game_nResult = IIf(Game_sdMax = 0, RESULT_BLACKWIN, RESULT_REDWIN)
                App_frmMain.lblResult = ResultString
                Engine_nStatus = IDLE_REST
                EndGame "WIN", IIf(Game_sdMax = 0, "t方J。", "黑方J。")
            ElseIf Engine_bDraw And Engine_bPonderFinishedDraw Then
                Game_nResult = RESULT_DRAW
                App_frmMain.lblResult = ResultString
                Engine_nStatus = IDLE_REST
                EndGame "DRAW", IIf(Game_sdMax = 0, "t方接受提和。", "黑方接受提和。")
            Else
                Engine_nStatus = IDLE_READY
                AddMove Engine_mvPonderFinished, MOVE_WITH_ECHO, MOVE_BY_ENGINE
                If Options_bAllowResign And Engine_bPonderFinishedDraw Then
                    Engine_bDraw = True
                    If Not IIf(Game_sdMax = 1, Engine_bBlack, Engine_bRed) Then
                        MsgBox IIf(Game_sdMax = 1, "t方提和。", "黑方提和。") + vbCrLf + "如果接受提和，x瘛半X”菜蔚摹疤岷/接受提和”功能。" + _
                                vbCrLf + "如果拒^提和，t可以不予理睬，^m下一著棋。", vbInformation
                    End If
                End If
                If Engine_mvPonderFinishedPonder <> 0 Then
                    Engine_mvPonder = Engine_mvPonderFinishedPonder
                    Engine_nStatus = BUSY_PONDER
                    RunEngine
                End If
            End If
        Else
            Engine_nStatus = IDLE_READY
        End If
    End If
    AddMove = True
Else
    If bEcho And (nMoveStatus And MOVE_INCHECK) <> 0 Then
        PlayWavSound "ILLEGAL"
    End If
    AddMove = False
End If

End Function

' 分析_局
Public Sub AnalyzeEcco()

Dim i As Integer, dwEcco As Long
Dim szMoveStr As String
If App_bAutoEcco And Game_bStartPos And Game_nMaxMove <= 20 Then
    szMoveStr = ""
    For i = 1 To Game_nMaxMove
        szMoveStr = szMoveStr + Game_szMoveText(1, i)
    Next
    dwEcco = EccoIndex(szMoveStr)
    Game_szEcco = Left(MkL(dwEcco), 3)
    Game_szOpen = AllocString(EccoOpening(dwEcco))
    Game_szVar = AllocString(EccoVariation(dwEcco))
    App_frmMain.lblOpen = OpenString
End If

End Sub

' 以上是界面控u例程

' 以下是引擎控u例程

' @示引擎信息的υ框
Public Sub AboutEngine()

MsgBoxWithIcon IIf(Engine_szName = "", "", "引擎：" + Engine_szName + vbCrLf) + _
        IIf(Engine_szVersion = "", "", "版本：" + Engine_szVersion + vbCrLf) + _
        IIf(Engine_szCopyright = "", "", "版啵" + Engine_szCopyright + vbCrLf) + _
        IIf(Engine_szAuthor = "", "", "作者：" + Engine_szAuthor + vbCrLf) + _
        IIf(Engine_szUser = "", "", "用簦" + Engine_szUser), "P於UCCI引擎", Engine_szFile

End Sub

' 根据引擎走子方翻D棋P并@示p方
Public Sub EnginePlayer()

Dim szName As String, szSlaveName As String
Engine_bPonder = False
App_frmMain.mnEnginePonder.Checked = False
App_frmMain.tlb.Buttons("EnginePonder").Value = tbrUnpressed
If Engine_bRed Eqv Engine_bBlack Then
    App_frmMain.mnEnginePonder.Enabled = False
    App_frmMain.tlb.Buttons("EnginePonder").Enabled = False
Else
    App_frmMain.mnEnginePonder.Enabled = True
    App_frmMain.tlb.Buttons("EnginePonder").Enabled = True
    If Options_bAlwaysPonder Then
        Engine_bPonder = True
        App_frmMain.mnEnginePonder.Checked = True
        App_frmMain.tlb.Buttons("EnginePonder").Value = tbrPressed
    End If
    If Options_bAutoFlip Then
        App_bFlipped = Engine_bRed
        CoordFlush
        BoardFlush
    End If
End If
App_frmMain.mnPosFlip.Checked = App_bFlipped
App_frmMain.tlb.Buttons("PosFlip").Value = IIf(App_bFlipped, tbrPressed, tbrUnpressed)
If Options_bEngineName Then
    szName = IIf(Engine_szName = "ElephantEye", "象棋巫", Engine_szName)
    szSlaveName = IIf(Engine_szSlaveName = "ElephantEye", "象棋巫", Engine_szSlaveName)
    Game_szRedPlayer = IIf(Engine_bRed, IIf(Engine_bSlaveRed, Engine_szSlaveName, Engine_szName), Options_szUser)
    Game_szBlackPlayer = IIf(Engine_bBlack, IIf(Engine_bSlaveBlack, Engine_szSlaveName, Engine_szName), Options_szUser)
End If
LabelFlush
StartTimer

End Sub

' 根据引擎是否在思考，@示菜魏凸ぞ谏系目捎霉δ
Public Sub EngineBusy(ByVal bBusy As Boolean)

App_frmMain.mnPosMirror.Enabled = Not bBusy
App_frmMain.mnPosEdit.Enabled = Not bBusy
App_frmMain.tlb.Buttons("PosEdit").Enabled = Not bBusy
App_frmMain.mnPosPaste.Enabled = Not bBusy
App_frmMain.mnPosLoad.Enabled = Not bBusy
App_frmMain.mnWizardPlay.Enabled = Not bBusy
App_frmMain.tlb.Buttons("WizardPlay").Enabled = Not bBusy
App_frmMain.mnWizardHandicap_.Enabled = Not bBusy
App_frmMain.mnEndgamesStart.Enabled = Not bBusy
App_frmMain.tlb.Buttons("EndgamesStart").Enabled = Not bBusy
App_frmMain.mnEndgames_.Enabled = Not bBusy
App_frmMain.mnEngineLevel.Enabled = Not bBusy
App_frmMain.tlb.Buttons("EngineLevel").Enabled = Not bBusy
App_frmMain.mnEngineSet.Enabled = Not bBusy
App_frmMain.mnEngineStop.Enabled = bBusy
App_frmMain.tlb.Buttons("EngineStop").Enabled = bBusy
If bBusy And App_bPlay Then
    App_bPlay = False
    App_frmMain.mnWizardPlay.Checked = False
    App_frmMain.tlb.Buttons("WizardPlay").Value = tbrUnpressed
End If

End Sub

' 向引擎l送指令，并日志，可以x袷侵饕擎是引擎
Public Sub SendEngine(ByVal szLineStr As String, Optional ByVal bSlaveEngine As Boolean = False)

Dim nLogFileNo As Integer
If bSlaveEngine Then
    PipeLineOutput Engine_pipeSlave, szLineStr
Else
    PipeLineOutput Engine_pipe, szLineStr
End If
If Engine_bEngineLog And Engine_szLogFile <> "" Then
    nLogFileNo = FreeFile
    On Error GoTo lnErrorOpen
    Open Engine_szLogFile For Append As #nLogFileNo
    Print #nLogFileNo, "<font color=""" + IIf(bSlaveEngine, "#FF00FF", "#FF0000") + """>" + szLineStr + "</font><br>"
    Close #nLogFileNo
End If

lnErrorOpen:
On Error GoTo 0

End Sub

' 把接收到的引擎反入日志
Public Sub ReceiveEngineLog(ByVal szLineStr As String, Optional ByVal bSlaveEngine As Boolean = False)

Dim nLogFileNo As Integer
If Engine_bEngineLog And Engine_szLogFile <> "" Then
    nLogFileNo = FreeFile
    On Error GoTo lnErrorOpen
    Open Engine_szLogFile For Append As #nLogFileNo
    Print #nLogFileNo, "<font color=""" + IIf(bSlaveEngine, "#000080", "#0000FF") + """>" + szLineStr + "</font><br>"
    Close #nLogFileNo
End If

lnErrorOpen:
On Error GoTo 0

End Sub

' 引擎思考，思考前必O置思考B(BUSY_ANALYZE/BUSY_THINK/BUSY_PONDER)
Public Sub RunEngine()

Dim i As Integer, nIrrevMove As Integer, bSlaveEngine As Boolean
Dim nTimeRatio As Long, nMoveStatus As Long
Dim szFenStr As String, szGoPonder As String
Dim nMoveList(1 To 128) As Long

Engine_posBoard = Game_posIrrev(Game_nCurrIrrev)
Engine_dwTime = Timer
Engine_nMoves = CchessGenMoves(Engine_posBoard, nMoveList(1))
Engine_nDepth = 0
Engine_nCurrMove = 0
If Engine_bAnalyze Then
    App_frmMain.txtThink.Text = ""
End If
szFenStr = Game_szIrrevFen(Game_nCurrIrrev)
nIrrevMove = Game_nCurrMove + 1 - Game_posIrrev(Game_nCurrIrrev).nMoveNum
If Game_nCurrMove > nIrrevMove Then
    szFenStr = szFenStr + " moves"
    For i = nIrrevMove + 1 To Game_nCurrMove
        szFenStr = szFenStr + " " + Move2Coord(Game_mvMove(i))
    Next
End If
szGoPonder = "go"
If Engine_nStatus = BUSY_PONDER Then
    Select Case Game_nFormat
    Case 0
        Engine_szPonderStr = MkC(CchessFile2Chin(CchessMove2File(Engine_mvPonder, Engine_posBoard), Engine_posBoard.sdPlayer))
    Case 1
        Engine_szPonderStr = MkL(CchessMove2File(Engine_mvPonder, Engine_posBoard))
    Case 2
        Engine_szPonderStr = Move2Iccs(Engine_mvPonder)
    End Select
    If CchessTryMove(Engine_posBoard, nMoveStatus, Engine_mvPonder) <> 0 Then
        szFenStr = szFenStr + IIf(Game_nCurrMove > nIrrevMove, "", " moves")
        szFenStr = szFenStr + " " + Move2Coord(Engine_mvPonder)
        szGoPonder = szGoPonder + " ponder"
    End If
Else
    If Engine_bDraw Then
        szGoPonder = szGoPonder + " draw"
    End If
End If
bSlaveEngine = IIf(Engine_posBoard.sdPlayer = 0, Engine_bSlaveRed, Engine_bSlaveBlack)
nTimeRatio = IIf(bSlaveEngine, Engine_nTimeRatioSlave, Engine_nTimeRatio)
SendEngine "position fen " + szFenStr, bSlaveEngine
If Engine_mvBan <> 0 Then
    SendEngine "banmoves " + Move2Coord(Engine_mvBan), bSlaveEngine
End If
If Engine_nStatus = BUSY_ANALYZE Then
    SendEngine "go infinite", bSlaveEngine
Else
    If Timer_nDepth <= 0 Then
        If Timer_bIncMode Then
            SendEngine szGoPonder + " time" + Str(nTimeRatio * Timer_nTimeLeft(Engine_posBoard.sdPlayer)) + _
                    " increment" + Str(IIf(Timer_nIncTime < 0, 0, nTimeRatio * Timer_nIncTime)) + _
                    " opptime" + Str(nTimeRatio * Timer_nTimeLeft(1 - Engine_posBoard.sdPlayer)) + _
                    " oppincrement" + Str(IIf(Timer_nIncTime < 0, 0, nTimeRatio * Timer_nIncTime)), bSlaveEngine
        Else
            SendEngine szGoPonder + " time" + Str(nTimeRatio * Timer_nTimeLeft(Engine_posBoard.sdPlayer)) + _
                    " movestogo" + Str(Timer_nMoveLeft(Engine_posBoard.sdPlayer)) + _
                    " opptime" + Str(nTimeRatio * Timer_nTimeLeft(1 - Engine_posBoard.sdPlayer)) + _
                    " oppmovestogo" + Str(Timer_nMoveLeft(1 - Engine_posBoard.sdPlayer)), bSlaveEngine
        End If
    Else
        SendEngine szGoPonder + " depth" + Str(Timer_nDepth), bSlaveEngine
    End If
End If
App_frmMain.stb.Panels("Curr").Text = ""
App_frmMain.stb.Panels("Nps").Text = ""
App_frmMain.stb.Panels("Pv").Text = ""
App_frmMain.stb.Panels("Msg").Text = ""
EngineBusy True

End Sub

' 在思考框中出信息，并定位光
Public Sub AppendThink(ByVal sz As String)

Dim nTextLen As Long
If App_frmMain.txtThink.Text = "" Then
    App_frmMain.txtThink.Text = sz
    App_frmMain.txtThink.SelStart = 0
Else
    App_frmMain.txtThink.Text = App_frmMain.txtThink.Text + vbCrLf
    nTextLen = Len(App_frmMain.txtThink.Text)
    App_frmMain.txtThink.Text = App_frmMain.txtThink.Text + sz
    App_frmMain.txtThink.SelStart = nTextLen
End If

End Sub

' 出引擎的信息，@示到@示思考窗口和B
Public Sub PopEngine(ByVal szReceived As String)

Dim i As Integer, nScore As Integer, nOldDepth As Integer
Dim nTime As Long, nNodes As Long
Dim szMoveList As String, nMoveStatus As Long, mv As Long
Dim pos As PositionStruct

' 出前思考著法
i = InStr(szReceived, " currmove ")
If i > 0 Then
    Engine_nCurrMove = Engine_nCurrMove + 1
    If Options_bStatusBar Then
        mv = Coord2Move(Mid(szReceived, i + 10))
        Select Case Game_nFormat
        Case 0
            szMoveList = MkC(CchessFile2Chin(CchessMove2File(mv, Engine_posBoard), Engine_posBoard.sdPlayer))
        Case 1
            szMoveList = MkL(CchessMove2File(mv, Engine_posBoard))
        Case 2
            szMoveList = Move2Iccs(mv)
        End Select
        App_frmMain.stb.Panels("Curr").Text = LTrim(Str(Engine_nDepth)) + " (" + LTrim(Str(Engine_nCurrMove)) + "/" + LTrim(Str(Engine_nMoves)) + ") " + szMoveList
    End If
End If

' 出速度
If Options_bStatusBar Then
    i = InStr(szReceived, " nodes ")
    If i > 0 Then
        nNodes = Str2Lng(Mid(szReceived, i + 7))
        nTime = (Timer - Engine_dwTime) * 1000
        nTime = IIf(nTime < 0, nTime + 86400000, nTime)
        If nTime > 0 Then
            App_frmMain.stb.Panels("Nps").Text = "NPS" + Str(nNodes \ nTime) + "K"
        End If
    End If
End If

' 出主要例
i = InStr(szReceived, " depth ")
If i > 0 Then
    nOldDepth = Engine_nDepth
    Engine_nDepth = Str2Int(Mid(szReceived, i + 7), -999, 999)
    If Engine_nDepth > nOldDepth Then
        Engine_nCurrMove = 0
    End If
    If Engine_nDepth > 0 Then
        If Engine_bAnalyze And Options_bEngineTime Then
            nTime = Int((Timer - Engine_dwTime) * 1000)
            nTime = IIf(nTime < 0, nTime + 86400000, nTime)
            AppendThink TimeText(nTime \ 1000) + "." + Right(Str(nTime \ 100), 1) + " ->"
        End If
    End If
    szMoveList = Space(4 - Len(Str(Engine_nDepth))) + LTrim(Str(Engine_nDepth))
    i = InStr(szReceived, " score ")
    If i > 0 Then
        nScore = Str2Int(Mid(szReceived, i + 7))
        If nScore > 0 Then
            szMoveList = szMoveList + Space(6 - Len(Str(nScore))) + "(+" + LTrim(Str(nScore)) + ")"
        ElseIf nScore < 0 Then
            szMoveList = szMoveList + Space(6 - Len(Str(nScore))) + "(" + Str(nScore) + ")"
        Else
            szMoveList = szMoveList + "     (0)"
        End If
        i = InStr(szReceived, " pv ")
        If i > 0 Then
            pos = Engine_posBoard
            If Engine_nStatus = BUSY_PONDER Then
                szMoveList = szMoveList + " (" + Engine_szPonderStr + ")"
            End If
            i = i + 4
            Do While i < Len(szReceived)
                mv = Coord2Move(Mid(szReceived, i))
                Select Case Game_nFormat
                Case 0
                    szMoveList = szMoveList + " " + MkC(CchessFile2Chin(CchessMove2File(mv, pos), pos.sdPlayer))
                Case 1
                    szMoveList = szMoveList + " " + MkL(CchessMove2File(mv, pos))
                Case 2
                    szMoveList = szMoveList + " " + Move2Iccs(mv)
                End Select
                CchessTryMove pos, nMoveStatus, mv
                i = i + 5
            Loop
        End If
        If Options_bStatusBar And Engine_nDepth > 0 Then
            App_frmMain.stb.Panels("Pv").Text = szMoveList
        End If
    End If
    If Engine_bAnalyze Then
        AppendThink szMoveList
    End If
End If

' 出引擎提示信息
i = InStr(szReceived, " message ")
If i > 0 Then
    If Options_bStatusBar Then
        App_frmMain.stb.Panels("Msg").Text = Mid(szReceived, i + 9)
    End If
    If Engine_bAnalyze Then
        AppendThink Mid(szReceived, i + 9)
    End If
End If

End Sub

' 在引擎思考r，接收引擎信息
Public Function ReceiveEngine() As Boolean

Dim lpStr As Long, bSlaveEngine As Boolean, szReceived As String
Dim i As Integer, mv As Long

If Engine_nStatus = IDLE_UNLOAD Then
    ReceiveEngine = False
    Exit Function
End If
bSlaveEngine = (Engine_nStatus >= BUSY_WAIT And IIf(Engine_posBoard.sdPlayer = 0, Engine_bSlaveRed, Engine_bSlaveBlack))
If bSlaveEngine Then
    lpStr = PipeLineInput(Engine_pipeSlave)
Else
    lpStr = PipeLineInput(Engine_pipe)
End If
If lpStr = 0 Then
    ReceiveEngine = False
Else
    szReceived = AllocString(lpStr)
    ReceiveEngineLog szReceived, bSlaveEngine
    If Left(szReceived, 9) = "bestmove " Then
        EngineBusy False
        mv = Coord2Move(Mid(szReceived, 10, 4))
        Select Case Engine_nStatus
        Case BUSY_WAIT
            Engine_nStatus = IDLE_READY
        Case BUSY_PONDER
            Engine_mvPonderFinished = mv
            If Engine_bPonder And Mid(szReceived, 14, 8) = " ponder " Then
                Engine_mvPonderFinishedPonder = Coord2Move(Mid(szReceived, 22, 4))
            Else
                Engine_mvPonderFinishedPonder = 0
            End If
            Engine_bPonderFinishedResign = (Right(szReceived, 7) = " resign")
            Engine_bPonderFinishedDraw = (Right(szReceived, 5) = " draw")
            Engine_nStatus = IDLE_PONDER
        Case BUSY_ANALYZE
            Engine_nStatus = IDLE_REST
        Case BUSY_THINK
            If Options_nMoveDelay > 0 Then
                Sleep Options_nMoveDelay * 1000
            End If
            Do While Game_nCurrMove < Game_nMaxMove
                MoveForward
            Loop
            If Options_bAllowResign And Right(szReceived, 7) = " resign" Then
                Game_nResult = IIf(Game_sdMax = 0, RESULT_BLACKWIN, RESULT_REDWIN)
                App_frmMain.lblResult = ResultString
                Timer_bEnabled = False
                App_frmMain.mnEngineLevel.Checked = False
                App_frmMain.tlb.Buttons("EngineLevel").Value = tbrUnpressed
                Engine_nStatus = IDLE_REST
                EndGame "WIN", IIf(Game_sdMax = 0, "t方J。", "黑方J。")
            ElseIf Engine_bDraw And Right(szReceived, 5) = " draw" Then
                Game_nResult = RESULT_DRAW
                App_frmMain.lblResult = ResultString
                Timer_bEnabled = False
                App_frmMain.mnEngineLevel.Checked = False
                App_frmMain.tlb.Buttons("EngineLevel").Value = tbrUnpressed
                Engine_nStatus = IDLE_REST
                EndGame "DRAW", IIf(Game_sdMax = 0, "t方接受提和。", "黑方接受提和。")
            ElseIf AddMove(mv, MOVE_WITH_ECHO, MOVE_BY_ENGINE) Then
                App_frmMain.SetFocus
                If Options_bAllowResign And Right(szReceived, 5) = " draw" Then
                    Engine_bDraw = True
                    If Not IIf(Game_sdMax = 1, Engine_bBlack, Engine_bRed) Then
                        MsgBox IIf(Game_sdMax = 1, "t方提和。", "黑方提和。") + vbCrLf + "如果接受提和，x瘛半X”菜蔚摹疤岷/接受提和”功能。" + _
                                vbCrLf + "如果拒^提和，t可以不予理睬，^m下一著棋。", vbInformation
                    End If
                End If
                If Engine_bPonder And Mid(szReceived, 14, 8) = " ponder " Then
                    Engine_mvPonder = Coord2Move(Mid(szReceived, 22, 4))
                    Engine_nStatus = BUSY_PONDER
                    RunEngine
                Else
                    Engine_nStatus = IDLE_READY
                End If
            Else
                ' 走棋]有成功，也停止引擎
                Engine_nStatus = IDLE_REST
            End If
        Case Else
            MsgBox "不接收到引擎的信息：" + szReceived, vbExclamation
        End Select
        If Options_bStatusBar Then
            App_frmMain.stb.Panels("Curr").Text = ""
        End If
    ElseIf Left(szReceived, 10) = "nobestmove" Then
        EngineBusy False
        Select Case Engine_nStatus
        Case BUSY_WAIT
            Engine_nStatus = IDLE_READY
        Case BUSY_PONDER
            Engine_nStatus = IDLE_READY
        Case BUSY_ANALYZE
            Engine_nStatus = IDLE_REST
        Case BUSY_THINK
            Engine_nStatus = IDLE_REST
        Case Else
            ' MsgBox "不接收到引擎的信息：" + szReceived, vbExclamation
        End Select
        If Options_bStatusBar Then
            App_frmMain.stb.Panels("Curr").Text = ""
        End If
    ElseIf Left(szReceived, 12) = "info string " Then
    ElseIf Left(szReceived, 5) = "info " Then
        PopEngine szReceived
    End If
    ReceiveEngine = True
End If

End Function

' 中止引擎思考
Public Sub StopEngine()

Dim nOldStatus As Integer
Dim dfLastTime As Double, dfThisTime As Double
SendEngine "stop", IIf(Engine_posBoard.sdPlayer = 0, Engine_bSlaveRed, Engine_bSlaveBlack)
dfLastTime = Timer
nOldStatus = Engine_nStatus
Do While Engine_nStatus = nOldStatus
    If Not ReceiveEngine Then
        Sleep 1
        dfThisTime = Timer
        dfThisTime = dfThisTime + IIf(dfThisTime < dfLastTime, 86400#, 0#)
        If dfThisTime > dfLastTime + 1# Then
            Exit Do
        End If
    End If
Loop
If Engine_nStatus = nOldStatus Then
    Engine_nStatus = IDLE_READY
    EngineBusy False
End If

End Sub

' O置引擎
Public Sub SetEngine(Optional ByVal bSlaveEngine As Boolean = False)

SendEngine "setoption promotion " + IIf(App_bPromotion, "true", "false"), bSlaveEngine
SendEngine "setoption usebook " + IIf(Engine_bUseBook, "true", "false"), bSlaveEngine
SendEngine "setoption useegtb " + IIf(Engine_bUseEgtb, "true", "false"), bSlaveEngine
If Engine_szBookFiles <> "" Then
    SendEngine "setoption bookfiles " + Engine_szBookFiles, bSlaveEngine
End If
If Engine_szEgtbPaths <> "" Then
    SendEngine "setoption egtbpaths " + Engine_szEgtbPaths, bSlaveEngine
End If
SendEngine "setoption hashsize" + IIf(Engine_nHash = 0, " 0", Str(16 * 2 ^ (Engine_nHash - 1))), bSlaveEngine
If Engine_nThreads = 0 Then
    SendEngine "setoption threads 0"
    SendEngine "setoption idle none"
ElseIf Engine_nThreads < 4 Then
    SendEngine "setoption threads 1"
    SendEngine "setoption idle " + Choose(Engine_nThreads, "large", "medium", "small"), bSlaveEngine
Else
    SendEngine "setoption threads" + Str(2 ^ (Engine_nThreads - 4)), bSlaveEngine
    SendEngine "setoption idle none"
End If
SendEngine "setoption randomness " + Choose(Engine_nRandomness + 1, "none", "small", "medium", "large"), bSlaveEngine
SendEngine "setoption style " + Choose(Engine_nStyle + 1, "solid", "normal", "risky"), bSlaveEngine

End Sub

' 根据引擎是否已加d，@示菜魏凸ぞ谏系目捎霉δ
Public Sub EngineLoaded(ByVal bLoaded As Boolean)

App_frmMain.mnEngineSet.Enabled = bLoaded
Engine_bRed = False
App_frmMain.mnEngineRed.Enabled = bLoaded
App_frmMain.mnEngineRed.Checked = False
App_frmMain.tlb.Buttons("EngineRed").Enabled = bLoaded
App_frmMain.tlb.Buttons("EngineRed").Value = tbrUnpressed
Engine_bBlack = False
App_frmMain.mnEngineBlack.Enabled = bLoaded
App_frmMain.mnEngineBlack.Checked = False
App_frmMain.tlb.Buttons("EngineBlack").Enabled = bLoaded
App_frmMain.tlb.Buttons("EngineBlack").Value = tbrUnpressed
Engine_bPonder = False
App_frmMain.mnEnginePonder.Enabled = False
App_frmMain.mnEnginePonder.Checked = False
App_frmMain.tlb.Buttons("EnginePonder").Enabled = False
App_frmMain.tlb.Buttons("EnginePonder").Value = tbrUnpressed
If Engine_bAnalyze Then
    StopAnalyze
End If
App_frmMain.mnEngineAnalyze.Enabled = bLoaded
App_frmMain.tlb.Buttons("EngineAnalyze").Enabled = bLoaded
App_frmMain.mnEngineStop.Enabled = False
App_frmMain.mnEngineRetract.Enabled = bLoaded
App_frmMain.mnEngineBan.Enabled = bLoaded
App_frmMain.mnEngineDraw.Enabled = bLoaded
App_frmMain.mnEngineDraw.Checked = False
App_frmMain.tlb.Buttons("EngineStop").Enabled = False
App_frmMain.mnHelpEngine.Enabled = bLoaded
If Not bLoaded Then
    Timer_bEnabled = False
    App_frmMain.mnEngineLevel.Checked = False
    App_frmMain.tlb.Buttons("EngineLevel").Value = tbrUnpressed
End If

End Sub

' P]引擎(如果有副引擎，先P]副引擎)
Public Sub CloseEngine()

Dim dfLastTime As Double, dfThisTime As Double, szReceived As String
Dim lpStr As Long
If Engine_nStatus > BUSY_WAIT Then
    Engine_nStatus = BUSY_WAIT
    StopEngine
End If
If Engine_bSlaveRed Or Engine_bSlaveBlack Then
    CloseSlaveEngine
End If
SendEngine "quit"
dfLastTime = Timer
Do While Engine_nStatus > IDLE_UNLOAD
    lpStr = PipeLineInput(Engine_pipe)
    If lpStr = 0 Then
        Sleep 1
        dfThisTime = Timer
        dfThisTime = dfThisTime + IIf(dfThisTime < dfLastTime, 86400#, 0#)
        If dfThisTime > dfLastTime + 1# Then
            Exit Do
        End If
    Else
        szReceived = AllocString(lpStr)
        ReceiveEngineLog szReceived
        If szReceived = "bye" Then
            Engine_nStatus = IDLE_UNLOAD
        End If
    End If
Loop
PipeClose Engine_pipe
If Engine_nStatus > IDLE_UNLOAD Then
    MsgBox "o法正常卸d引擎！", vbExclamation
    Engine_nStatus = IDLE_UNLOAD
End If

End Sub

' P]副引擎
Public Sub CloseSlaveEngine()

Dim dfLastTime As Double, dfThisTime As Double, szReceived As String
Dim lpStr As Long
SendEngine "quit", SLAVE_ENGINE
dfLastTime = Timer
Do While (Engine_bSlaveRed Or Engine_bSlaveBlack)
    lpStr = PipeLineInput(Engine_pipeSlave)
    If lpStr = 0 Then
        Sleep 1
        dfThisTime = Timer
        dfThisTime = dfThisTime + IIf(dfThisTime < dfLastTime, 86400#, 0#)
        If dfThisTime > dfLastTime + 1# Then
            Exit Do
        End If
    Else
        szReceived = AllocString(lpStr)
        ReceiveEngineLog szReceived, SLAVE_ENGINE
        If szReceived = "bye" Then
            Engine_bSlaveRed = False
            Engine_bSlaveBlack = False
        End If
    End If
Loop
PipeClose Engine_pipeSlave
If Engine_bSlaveRed Or Engine_bSlaveBlack Then
    MsgBox "o法正常卸d引擎！", vbExclamation
    Engine_bSlaveRed = False
    Engine_bSlaveBlack = False
End If

End Sub

' 打_引擎
Public Sub OpenEngine(Optional ByVal bInfo As Boolean = False)

Dim bLoaded As Boolean
Dim dfLastTime As Double, dfThisTime As Double
Dim lpStr As Long, szReceived As String
If Engine_nStatus > IDLE_UNLOAD Then
    CloseEngine
End If
If Engine_szFile = "" Then
    bLoaded = False
ElseIf GetAppType(Engine_szFile) <> IMAGE_SUBSYSTEM_WINDOWS_CUI Then
    bLoaded = False
Else
    PipeOpen Engine_pipe, Engine_szFile
    SendEngine "ucci"
    dfLastTime = Timer
    Engine_szName = ""
    Engine_szVersion = ""
    Engine_szCopyright = ""
    Engine_szAuthor = ""
    Engine_szUser = ""
    Engine_nTimeRatio = 1
    Do While Engine_nStatus = IDLE_UNLOAD
        lpStr = PipeLineInput(Engine_pipe)
        If lpStr = 0 Then
            Sleep 1
            dfThisTime = Timer
            dfThisTime = dfThisTime + IIf(dfThisTime < dfLastTime, 86400#, 0#)
            ' 加载引擎的时间延长到30秒
            If dfThisTime > dfLastTime + 30# Then
                Exit Do
            End If
        Else
            szReceived = AllocString(lpStr)
            ReceiveEngineLog szReceived
            If Left(szReceived, 8) = "id name " Then
                Engine_szName = Mid(szReceived, 9)
            ElseIf Left(szReceived, 11) = "id version " Then
                Engine_szVersion = Mid(szReceived, 12)
            ElseIf Left(szReceived, 13) = "id copyright " Then
                Engine_szCopyright = Mid(szReceived, 14)
            ElseIf Left(szReceived, 10) = "id author " Then
                Engine_szAuthor = Mid(szReceived, 11)
            ElseIf Left(szReceived, 8) = "id user " Then
                Engine_szUser = Mid(szReceived, 9)
            ElseIf Left(szReceived, 19) = "option usemillisec " Then
                Engine_nTimeRatio = 1000
            ElseIf szReceived = "ucciok" Then
                Engine_nStatus = IDLE_READY
            End If
        End If
    Loop
    If Engine_nStatus > IDLE_UNLOAD Then
        bLoaded = True
        Engine_bSlaveRed = False
        Engine_bSlaveBlack = False
        If Engine_nTimeRatio = 1000 Then
            SendEngine "setoption usemillisec true"
        End If
        SetEngine
    Else
        SendEngine "quit"
        PipeClose Engine_pipe
        bLoaded = False
    End If
End If
If bInfo Then
    If bLoaded Then
        AboutEngine
    Else
        If Engine_szFile = "" Then
            MsgBox "]有加d引擎！", vbExclamation
        Else
            MsgBox Engine_szFile + " 不是中象棋引擎文件！", vbExclamation
            Engine_szFile = ""
        End If
    End If
End If
EngineLoaded bLoaded
SetTitle

End Sub

' 打_副引擎
Public Sub OpenSlaveEngine()

Dim szEngineFile As String, szCopyright As String, szAuthor As String, szUser As String
Dim dfLastTime As Double, dfThisTime As Double
Dim lpStr As Long, szReceived As String

If Engine_bSlaveRed Or Engine_bSlaveBlack Then
    CloseSlaveEngine
End If
szEngineFile = FileDialog("加d副引擎", "引擎文件", "EXE")
If szEngineFile = "" Then
    MsgBox "]有加d引擎！", vbExclamation
ElseIf GetAppType(szEngineFile) <> IMAGE_SUBSYSTEM_WINDOWS_CUI Then
    MsgBox szEngineFile + " 不是中象棋引擎文件！", vbExclamation
Else
    PipeOpen Engine_pipeSlave, szEngineFile
    SendEngine "ucci", SLAVE_ENGINE
    dfLastTime = Timer
    Engine_szSlaveName = ""
    szCopyright = ""
    szAuthor = ""
    szUser = ""
    Engine_nTimeRatioSlave = 1
    Do While Not Engine_bSlaveBlack
        lpStr = PipeLineInput(Engine_pipeSlave)
        If lpStr = 0 Then
            Sleep 1
            dfThisTime = Timer
            dfThisTime = dfThisTime + IIf(dfThisTime < dfLastTime, 86400#, 0#)
            If dfThisTime > dfLastTime + 10# Then
                Exit Do
            End If
        Else
            szReceived = AllocString(lpStr)
            ReceiveEngineLog szReceived, SLAVE_ENGINE
            If Left(szReceived, 8) = "id name " Then
                Engine_szSlaveName = Mid(szReceived, 9)
            ElseIf Left(szReceived, 13) = "id copyright " Then
                szCopyright = Mid(szReceived, 14)
            ElseIf Left(szReceived, 10) = "id author " Then
                szAuthor = Mid(szReceived, 11)
            ElseIf Left(szReceived, 8) = "id user " Then
                szUser = Mid(szReceived, 9)
            ElseIf Left(szReceived, 19) = "option usemillisec " Then
                Engine_nTimeRatioSlave = 1000
            ElseIf szReceived = "ucciok" Then
                Engine_bSlaveBlack = True
            End If
        End If
    Loop
    If Engine_bSlaveBlack Then
        MsgBoxWithIcon IIf(Engine_szSlaveName = "", "", "引擎：" + Engine_szSlaveName + vbCrLf) + _
                IIf(szCopyright = "", "", "版啵" + szCopyright + vbCrLf) + _
                IIf(szAuthor = "", "", "作者：" + szAuthor + vbCrLf) + _
                IIf(szUser = "", "", "用簦" + szUser), "P於UCCI引擎", szEngineFile
        If Engine_nTimeRatioSlave = 1000 Then
            SendEngine "setoption usemillisec true", SLAVE_ENGINE
        End If
        SetEngine SLAVE_ENGINE
    Else
        SendEngine "quit", SLAVE_ENGINE
        PipeClose Engine_pipeSlave
        MsgBox szEngineFile + " 不是中象棋引擎文件！", vbExclamation
    End If
End If

End Sub

' r
Public Sub StartTimer()

Timer_bOver(0) = False
Timer_bOver(1) = False
Timer_nTimeLeft(0) = Timer_nInitTime * 60
Timer_nTimeLeft(1) = Timer_nInitTime * 60
Timer_nMoveLeft(0) = Timer_nIncMove
Timer_nMoveLeft(1) = Timer_nIncMove
SetTimer 0
SetTimer 1
If Timer_nDepth = 0 And (Engine_bRed Or Engine_bBlack) Then
    Timer_dfLastTime = Timer
    Timer_bEnabled = True
    App_frmMain.mnEngineLevel.Checked = True
    App_frmMain.tlb.Buttons("EngineLevel").Value = tbrPressed
Else
    Timer_bEnabled = False
    App_frmMain.mnEngineLevel.Checked = False
    App_frmMain.tlb.Buttons("EngineLevel").Value = tbrUnpressed
End If

End Sub

' 新局rx褚擎走哪方
Public Sub EngineNewGame(Optional ByVal nEnginePlay As Integer = -1)

If Engine_nStatus > IDLE_UNLOAD Then
    Select Case IIf(nEnginePlay < 0, Options_nEnginePlay, nEnginePlay)
    Case ENGINE_PLAY_NONE
        Engine_bRed = False
        Engine_bBlack = False
    Case ENGINE_PLAY_RED
        Engine_bRed = True
        Engine_bBlack = False
    Case ENGINE_PLAY_BLACK
        Engine_bRed = False
        Engine_bBlack = True
    Case ENGINE_PLAY_TURN
        If App_bRedTurn Then
            Engine_bRed = True
            Engine_bBlack = False
        Else
            Engine_bRed = False
            Engine_bBlack = True
        End If
        App_bRedTurn = Not App_bRedTurn
    Case ENGINE_PLAY_RANDOM
        If Rnd < 0.5 Then
            Engine_bRed = True
            Engine_bBlack = False
        Else
            Engine_bRed = False
            Engine_bBlack = True
        End If
    End Select
    App_frmMain.mnEngineRed.Checked = Engine_bRed
    App_frmMain.tlb.Buttons("EngineRed").Value = IIf(Engine_bRed, tbrPressed, tbrUnpressed)
    App_frmMain.mnEngineBlack.Checked = Engine_bBlack
    App_frmMain.tlb.Buttons("EngineBlack").Value = IIf(Engine_bBlack, tbrPressed, tbrUnpressed)
    If Engine_bRed Or Engine_bBlack Then
        EnginePlayer
    End If
End If

End Sub

' 引擎分析_始
Public Sub StartAnalyze()

Engine_bAnalyze = True
App_frmMain.mnEngineAnalyze.Checked = True
App_frmMain.tlb.Buttons("EngineAnalyze").Value = tbrPressed
App_frmMain.strpRight.Tabs.Item(2).Selected = True
App_frmMain.frmInfo.Visible = False
App_frmMain.frmThink.Visible = True
App_frmMain.frmMove2.Move App_frmMain.frmMove2.Left, App_frmMain.frmMove2.Top + IIf(Options_bLargeGui, 2760, 1440)

End Sub

' 引擎分析Y束
Public Sub StopAnalyze()

If Not (Engine_bRed Or Engine_bBlack) Then
    If Engine_nStatus > BUSY_WAIT Then
        Engine_nStatus = BUSY_WAIT
        StopEngine
    End If
    Engine_nStatus = IDLE_READY
End If
Engine_bAnalyze = False
App_frmMain.mnEngineAnalyze.Checked = False
App_frmMain.tlb.Buttons("EngineAnalyze").Value = tbrUnpressed
App_frmMain.strpRight.Tabs.Item(1).Selected = True
App_frmMain.frmInfo.Visible = True
App_frmMain.frmThink.Visible = False
App_frmMain.frmMove2.Move App_frmMain.frmMove2.Left, App_frmMain.frmMove2.Top - IIf(Options_bLargeGui, 2760, 1440)

End Sub

' 以上是引擎控u例程

' 以下是D片和音的理例程

' O置[藏棋P(小)
Public Sub BoardSettingSmallHide(ByVal nBoardIndex As Integer)

On Error Resume Next
frmHide.imgBoardSmall.Picture = LoadPicture(App_szPath + "IMAGES_S\" + _
        Choose(nBoardIndex + 1, "WOOD", "GREEN", "WHITE", "SHEET", "CANVAS", "DROPS", _
        "CLOUDS", "XQSTUDIO", "MOVESKY", "MRSJ", "ZMBL", "QIANHONG") + ".GIF")
On Error GoTo 0

End Sub

' O置棋P(小)
Public Sub BoardSettingSmall(ByVal nBoardIndex As Integer)

Dim i As Integer
BoardSettingSmallHide nBoardIndex
frmMainSmall.imgBoard.Picture = frmHide.imgBoardSmall.Picture
For i = 0 To BOARD_S_MAX - 1
    frmMainSmall.mnOptionsBoard(i).Checked = (nBoardIndex = i)
Next
Options_nBoardS = nBoardIndex

End Sub

' O置[藏棋P(大)
Public Sub BoardSettingLargeHide(ByVal nBoardIndex As Integer)

On Error Resume Next
frmHide.imgBoardLarge.Picture = LoadPicture(App_szPath + "IMAGES_L\" + _
        Choose(nBoardIndex + 1, "WOOD", "GREEN", "WHITE", "SHEET", "CANVAS", "DROPS", "QIANHONG") + ".GIF")
On Error GoTo 0

End Sub

' O置棋P(大)
Public Sub BoardSettingLarge(ByVal nBoardIndex As Integer)

Dim i As Integer
BoardSettingLargeHide nBoardIndex
frmMainLarge.imgBoard.Picture = frmHide.imgBoardLarge
For i = 0 To BOARD_L_MAX - 1
    frmMainLarge.mnOptionsBoard(i).Checked = (nBoardIndex = i)
Next
Options_nBoardL = nBoardIndex

End Sub

' @得棋子D片的文件名
Public Function PieceFileName(ByVal nIndex As Integer) As String

If nIndex = 30 Then
    PieceFileName = "RKM"
ElseIf nIndex = 31 Then
    PieceFileName = "BKM"
ElseIf nIndex >= 15 Then
    PieceFileName = PieceFileName(nIndex - 15) + "S"
Else
    If nIndex = 0 Then
        PieceFileName = "OO"
    Else
        If nIndex > 7 Then
            PieceFileName = "B" + PieceByte(nIndex - 8)
        Else
            PieceFileName = "R" + PieceByte(nIndex - 1)
        End If
    End If
End If

End Function

' O置[藏棋子(小)
Public Sub PiecesSettingSmallHide(ByVal nPiecesIndex As Integer)

Dim i As Integer, sz As String
sz = App_szPath + "IMAGES_S\" + Choose(nPiecesIndex + 1, "WOOD", "DELICATE", "POLISH", "XQSTUDIO", "MOVESKY", "MRSJ", "ZMBL") + "\"
On Error Resume Next
For i = 0 To 31
    frmHide.imgPiecesSmall(i).Picture = LoadPicture(sz + PieceFileName(i) + ".GIF")
Next
On Error GoTo 0

End Sub

' O置棋子(小)
Public Sub PiecesSettingSmall(ByVal nPiecesIndex As Integer)

Dim i As Integer
PiecesSettingSmallHide nPiecesIndex
For i = 0 To 31
    Set App_picPieces(i) = frmHide.imgPiecesSmall(i).Picture
Next
For i = 0 To PIECES_S_MAX - 1
    frmMainSmall.mnOptionsPieces(i).Checked = (nPiecesIndex = i)
Next
BoardFlush
LabelFlush
Options_nPiecesS = nPiecesIndex

End Sub

' O置[藏棋子(大)
Public Sub PiecesSettingLargeHide(ByVal nPiecesIndex As Integer)

Dim i As Integer, sz As String
sz = App_szPath + "IMAGES_L\" + Choose(nPiecesIndex + 1, "WOOD", "DELICATE", "POLISH") + "\"
On Error Resume Next
For i = 0 To 31
    frmHide.imgPiecesLarge(i).Picture = LoadPicture(sz + PieceFileName(i) + ".GIF")
Next
On Error GoTo 0

End Sub

' O置棋子(大)
Public Sub PiecesSettingLarge(ByVal nPiecesIndex As Integer)

Dim i As Integer
PiecesSettingLargeHide nPiecesIndex
For i = 0 To 31
    Set App_picPieces(i) = frmHide.imgPiecesLarge(i).Picture
Next
For i = 0 To PIECES_L_MAX - 1
    frmMainLarge.mnOptionsPieces(i).Checked = (nPiecesIndex = i)
Next
BoardFlush
LabelFlush
Options_nPiecesL = nPiecesIndex

End Sub

' O置音
Public Sub MusicSetting(ByVal nMusicIndex As Integer)

Dim i As Integer
frmHide.mci.Command = "Close"
If nMusicIndex < MUSIC_NONE Then
    frmHide.mci.FileName = App_szPath + "MUSICS\" + _
            Choose(nMusicIndex + 1, "EXPRESS", "FUNNY", "CLASSIC", "MOZART1", "MOZART4", _
            "FURELISE", "LOVDREAM", "WALTZ", "HUMOUR", "PAL", "CMUSIC") + ".MID"
    If Not App_bPrevInstance Then
        frmHide.mci.Command = "Open"
        frmHide.mci.Command = "Play"
    End If
End If
For i = 0 To MUSIC_MAX - 1
    App_frmMain.mnOptionsMusic(i).Checked = (nMusicIndex = i)
Next
Options_nMusic = nMusicIndex

End Sub

' 以上是D片和音的理例程

' 以下是翟O置例程

' 加d“日e月累”
Public Sub LoadTips()

Gui_szTips(1) = "　　象棋巫是一款功能超的象棋教W、X弈和棋V件，具有方便的人C稹⑵遄V、欣p、l布、管理棋局的功能，戎么罅肯笃辶}，完全可以M足各哟蜗笃酆谜摺⑵迨帧⒉门T及象棋文字工作者的各N需要。"
Gui_szTips(2) = "　　象棋巫支持多通用象棋引擎，他大都具有I棋手的水平，是象棋酆谜吆I棋手提高棋的良益友。" + vbCrLf + _
        "　　如果您ο笃逦自У囊擎 ElephantEye 的表F不M意，可以登 http://www.elephantbase.net/league.htm ，下d到其他各具特色的引擎。"
Gui_szTips(3) = "　　c簟靶碌钠寰帧卑粹o，就可以展_人C弈，象棋巫提供了五N的y度和四N的子模式o用趔w。"
Gui_szTips(4) = "　　如果您不希望X走棋，而HH是想知道X在想些什么，或者Xo你一些l，可以c簟半X分析”按o，打_思考窗口。" + vbCrLf + _
        "　　您可以把思考窗口中的思考路}u下恚粘N到主窗口中，λ伎季路M行推演。"
Gui_szTips(5) = "　　c簟鞍l布棋V”按o，就可以在W上和棋友交流棋V，或者向高手求助。" + vbCrLf + _
        "　　象棋巫推]了著名的象棋，Ko用籼峁┛旖莸陌l帖引В很方便地用粼谶@些象棋上l布棋V或l起求助。"
Gui_szTips(6) = "　　象棋巫目前主要通^Wjl行，最新的版本可以摹断笃灏倏迫W》@得：" + vbCrLf + "　　http://www.elephantbase.net/"
Gui_szTips(7) = "　　目前Wj上已有狄酝蛴通用格式的棋V文件(後Y.PGN)可供下d。您可以到以下面下d近20年热象棋比的棋V：http://www.elephantbase.net/resource.htm" + vbCrLf + _
        "　　同r也g迎您⒆约貉u作的文件上鹘o我。"
Gui_szTips(8) = "　　如果您ο笃逦有什么意或建h，可以跟象棋巫_lF系，]件地址是：" + vbCrLf + "　　webmaster@elephantbase.net" + vbCrLf + _
        "　　象棋巫_lF非常希望和大家保持系。"
Gui_szTips(9) = "　　您可以p糇烂嫔系南笃逦D(由棋子“象”、棋P和巫棒M成)直接酉笃逦。"
Gui_szTips(10) = "　　如果您是一象棋巫的初W者，您仔x本件“椭”x沃械摹椭主}”并多加，祝您早日精通象棋巫的各种功能。"
Gui_szTips(11) = "　　象棋巫支持文件P功能，pPGN文件(D耸瞧遄印跋唷)可以打_棋V，pFEN文件(D耸瞧灞P)可以打_局面。"
Gui_szTips(12) = "　　象棋巫支持“象棋演播室”的棋V。只要pXQF文件(D耸恰跋蟆弊)，或者c簟按蜷_”按o并x褚XQF文件，象棋巫就打_@棋V。"
Gui_szTips(13) = "　　象棋巫最适合800x600和1024x768的@示分q率，在“xO置”υ框(“x”x沃械摹霸O置”一)，把“界面”O成“大”，就可在1024x768的@示分q率下得到最舒适的@示效果。"
Gui_szTips(14) = "　　c簟安榭次谋酒遄V”按o，就可以看到棋V文本，檎迟N到BBS上或者Word文n中提供了方便。"
Gui_szTips(15) = "　　象棋巫可x入、弈天、中游蛑行牡戎象棋的存P文件和大部分的文本棋V文件，把@些含有著法的文本}u到剪N板，c簟募糍N板入”按o，@些著法就u到象棋巫中。"
Gui_szTips(16) = "　　象棋巫提供的棋VIP入法橄gIP入棋V的棋友提供了一种高效的棋V入方法，棋友可根据自己的T在底中℃IP或WXF字母格式中任意x瘛"
Gui_szTips(17) = "　　在欣p棋V的^程中，c簟安シ牌寰帧卑粹o就可脑步自友菔荆演示的rgg隔可以自由{整。再次c簟安シ牌寰帧卑粹o可停止自友菔尽"
Gui_szTips(18) = "　　在欣p棋V的^程中，可以使用棋P倒置和左右{Q功能牟煌的角度欣p棋局。其中左右{Q功能可以一次㈩似於“炮八平五”_局@拥钠遄V自愚DQ成大家T的“炮二平五”。反之亦然。"
Gui_szTips(19) = "　　如果在欣p棋Vr遇到}s的局面，可以c簟胺治鼍置妗卑粹o，前棋局新_O一象棋巫的窗口，自由推演後m的化。"
Gui_szTips(20) = "　　在象棋巫窗口中，`活\用“查看文本棋V”和“募糍N板入”功能，可⑼蒲萜灞P中的棋V入到前棋局中。"
Gui_szTips(21) = "　　象棋巫是自由件，如果您X得好用，可以自由地⒈拒件上d到任一共享件下d站c。" + vbCrLf + _
        "　　象棋巫的源程序l布在 http://sourceforge.net/projects/xqwizard/ @面上，您可以根据自己的需要υ闯绦蜻M行修改，并依照GPLfhl布您修改^的源程序。"
Gui_szTips(22) = "　　象棋巫在_l^程中得到了V大棋迷朋友的崆橹С郑作者在此表示衷心的感x，同r也希望您^m支持象棋巫，P注 http://www.elephantbase.net/ 。" + vbCrLf + _
        "　　您也可以⒈拒件的主接到自己的站c上。您使用 http://www.elephantbase.net/ 作殒接的路健"

Gui_szQuotes(1) = "　　子曰：“W而r之，不亦f乎？有朋自h方恚不亦泛酰咳瞬恢而不C，不亦君子乎？”" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《Z・W而》"
Gui_szQuotes(2) = "　　子曰：“有一言而可以K身行之者乎？”子曰：“其恕乎！己所不欲，勿施於人。”" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《Z・l`公》"
Gui_szQuotes(3) = "　　或曰：“以德笤梗何如？”子曰：“何以蟮拢恳灾笤梗以德蟮隆！" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《Z・》"
Gui_szQuotes(4) = "　　子夏曰：“博W而V志，切而近思；仁在其中矣。”" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《Z・子》"
Gui_szQuotes(5) = "　　子曰：“不患人之不己知，患不知人也。”" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《Z・W而》"
Gui_szQuotes(6) = "　　子曰：“三人行，必有我焉：衿渖普叨闹，其不善者而改之。”" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《Z・述而》"
Gui_szQuotes(7) = "　　⒂歙之，必固之；⒂弱之，必固之；⒂U之，必固d之；⒂取之，必固与之。是^微明。" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《老子・第三十六章》"
Gui_szQuotes(8) = "　　柔弱胜。~不可於Y，之利器不可以示人。" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《老子・第三十六章》"
Gui_szQuotes(9) = "　　Dy於其易；榇箪镀浼。天下y事，必作於易；天下大事，必作於。" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《老子・第六十三章》"
Gui_szQuotes(10) = "　　合抱之木，生於毫末；九又_，起於累土；千里之行，始於足下。" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《老子・第六十四章》"
Gui_szQuotes(11) = "　　知彼知己，胜乃不殆；知天知地，胜乃可全。" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《O子兵法・地形》"
Gui_szQuotes(12) = "　　怒可以}喜，C可以}f，亡不可以}存，死者不可以}生。" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《O子兵法・火攻》"

End Sub

' 加d局
Public Sub LoadEndgames(ByVal szEpdName As String, ByVal szTitle As String)

Dim nFileNo As Integer, i As Integer, sz As String
nFileNo = FreeFile
On Error GoTo lnErrorOpen
Open App_szPath + "ENDGAMES\" + szEpdName + ".EPD" For Input As #nFileNo
On Error GoTo 0
App_nEndgames = 0
Do While App_nEndgames < MAX_ENDGAMES And Not EOF(nFileNo)
    Line Input #nFileNo, sz
    i = InStr(sz, ";")
    If i > 0 Then
        App_szEndgames(App_nEndgames) = RTrim(Left(sz, i - 1))
        frmEndgames.lstGames.AddItem LTrim(Mid(sz, i + 1))
        App_nEndgames = App_nEndgames + 1
    End If
Loop
Close #nFileNo
If App_nEndgames = 0 Then
    MsgBox "o法打_文件 " + App_szPath + "ENDGAMES\" + szEpdName + ".EPD", vbExclamation
    ' Unload frmEndgames ' App_nEndgames = 0 f明 frmEndgames 未被加d^
Else
    If Gui_nEndgamesIndex >= App_nEndgames Then
        Gui_nEndgamesIndex = App_nEndgames - 1
    End If
    frmEndgames.lstGames.ListIndex = Gui_nEndgamesIndex
    frmEndgames.Caption = szTitle
    frmEndgames.Show vbModal, App_frmMain
    Gui_szEpdName = szEpdName
    Gui_szEpdTitle = szTitle
End If

Exit Sub
lnErrorOpen:
On Error GoTo 0
MsgBox "o法打_文件 " + App_szPath + "ENDGAMES\" + szEpdName + ".EPD", vbExclamation

End Sub

' 淖员砑虞d
Public Sub LoadRegs()

Options_bLargeGui = (Str2Int(GetSetting("XQWizard", "Options", "LargeGui", IIf(Screen.Height > 10000, "1", "0"))) <> 0)
Options_bSounds = (Str2Int(GetSetting("XQWizard", "Options", "Sounds", "1")) <> 0)
Options_bAutoPlay = (Str2Int(GetSetting("XQWizard", "Options", "AutoPlay", "0")) <> 0)
Options_nPlayIntv = Str2Int(GetSetting("XQWizard", "Options", "PlayIntv", "1"), 1, 10)
Options_bEscapeDouble = (Str2Int(GetSetting("XQWizard", "Options", "EscapeDouble", "0")) <> 0)
Options_bAnsiText = (Str2Int(GetSetting("XQWizard", "Options", "AnsiText", "0")) <> 0)
Options_bMonoBitmap = (Str2Int(GetSetting("XQWizard", "Options", "MonoBitmap", "0")) <> 0)
Options_bPrintDigit = (Str2Int(GetSetting("XQWizard", "Options", "PrintDigit", "1")) <> 0)
Options_nFlashXQ = Str2Int(GetSetting("XQWizard", "Options", "FlashXQ", "1"), FLASHXQ_SMALL, FLASHXQ_LARGE)
Options_nMoveDelay = Str2Int(GetSetting("XQWizard", "Options", "MoveDelay", "1"), 0, 5)
Options_bAutoFlip = (Str2Int(GetSetting("XQWizard", "Options", "AutoFlip", "1")) <> 0)
Options_bEngineName = (Str2Int(GetSetting("XQWizard", "Options", "EngineName", "1")) <> 0)
Options_bAlwaysPonder = (Str2Int(GetSetting("XQWizard", "Options", "AlwaysPonder", "0")) <> 0)
Options_bAllowResign = (Str2Int(GetSetting("XQWizard", "Options", "AllowResign", "1")) <> 0)
Options_bEngineTime = (Str2Int(GetSetting("XQWizard", "Options", "EngineTime", "0")) <> 0)
Options_bStatusBar = (Str2Int(GetSetting("XQWizard", "Options", "StatusBar", "1")) <> 0)
Options_nEnginePlay = Str2Int(GetSetting("XQWizard", "Options", "EnginePlay", "3"), 0, ENGINE_PLAY_MAX - 1)
Options_nBoardS = Str2Int(GetSetting("XQWizard", "Options", "BoardSmall", "0"), 0, BOARD_S_MAX - 1)
Options_nPiecesS = Str2Int(GetSetting("XQWizard", "Options", "PiecesSmall", "0"), 0, PIECES_S_MAX - 1)
Options_nBoardL = Str2Int(GetSetting("XQWizard", "Options", "BoardLarge", "0"), 0, BOARD_L_MAX - 1)
Options_nPiecesL = Str2Int(GetSetting("XQWizard", "Options", "PiecesLarge", "0"), 0, PIECES_L_MAX - 1)
Options_nMusic = Str2Int(GetSetting("XQWizard", "Options", "Music", "8"), 0, MUSIC_MAX - 1)
Options_bTopMost = (Str2Int(GetSetting("XQWizard", "Options", "TopMost", "0")) <> 0)
Options_bPromptSave = (Str2Int(GetSetting("XQWizard", "Options", "PromptSave", "1")) <> 0)
Options_bAutoSave = (Str2Int(GetSetting("XQWizard", "Options", "AutoSave", "0")) <> 0)
Options_szUser = GetSetting("XQWizard", "Options", "User", "象棋巫yF")
Options_bShowNew = (Str2Int(GetSetting("XQWizard", "Options", "ShowNew", "1")) <> 0)
Gui_nLeft = Str2Int(GetSetting("XQWizard", "Gui", "Left", "480"), 0, Int(Screen.Width * 0.75))
Gui_nTop = Str2Int(GetSetting("XQWizard", "Gui", "Top", "480"), 0, Int(Screen.Height * 0.75))
Gui_nEndgamesIndex = Str2Int(GetSetting("XQWizard", "Gui", "EndgamesIndex", "0"), 0, MAX_ENDGAMES - 1)
Gui_szEpdName = GetSetting("XQWizard", "Gui", "EpdName", "FUNNY")
Gui_szEpdTitle = GetSetting("XQWizard", "Gui", "EpdTitle", "趣味象棋")
Gui_bShowHowTo = (Str2Int(GetSetting("XQWizard", "Gui", IIf(App_bPromotion, "ShowHowTo2", "ShowHowTo"), "1")) <> 0)
Gui_nTipIndex = Str2Int(GetSetting("XQWizard", "Gui", "TipIndex", "1"), 1, TIP_NUM)
Gui_szUpgradeVer = GetSetting("XQWizard", "Gui", "UpgradeVer", STRING_VERSION)
Gui_szUpgradeDate = GetSetting("XQWizard", "Gui", "UpgradeDate", "")
Gui_szAdvertDate = GetSetting("XQWizard", "Gui", "AdvertDate", "")
Publish_nSize = Str2Int(GetSetting("XQWizard", "Publish", "Size", "0"), 0, PUB_SIZE_MAX - 1)
Publish_bTrad = (Str2Int(GetSetting("XQWizard", "Publish", "Trad", "1")) <> 0)
Publish_nBoardS = Str2Int(GetSetting("XQWizard", "Publish", "BoardS", "0"), 0, BOARD_S_MAX - 1)
Publish_nPiecesS = Str2Int(GetSetting("XQWizard", "Publish", "PiecesS", "0"), 0, PIECES_S_MAX - 1)
Publish_nBoardL = Str2Int(GetSetting("XQWizard", "Publish", "BoardL", "0"), 0, BOARD_L_MAX - 1)
Publish_nPiecesL = Str2Int(GetSetting("XQWizard", "Publish", "PiecesL", "0"), 0, PIECES_L_MAX - 1)
Publish_bContent = (Str2Int(GetSetting("XQWizard", "Publish", "Content", "0")) <> 0)
Publish_nLocation = Str2Int(GetSetting("XQWizard", "Publish", "Location", "0"), 0, PUB_LOCATION_MAX - 1)
Publish_nFormat = Str2Int(GetSetting("XQWizard", "Publish", "Format", "0"), 0, PUB_FORMAT_MAX - 1)
Timer_nDepth = Str2Int(GetSetting("XQWizard", "Timer", "Depth", "-1"), LEVEL_GRANDMASTER, LEVEL_INFINITE)
Timer_bIncMode = Str2Int(GetSetting("XQWizard", "Timer", "IncMode", "0")) <> 0
Timer_nInitTime = Str2Int(GetSetting("XQWizard", "Timer", "InitTime", "1"), 1, 480)
Timer_nIncMove = Str2Int(GetSetting("XQWizard", "Timer", "IncMove", "999"), 1, 999)
Timer_nIncTime = Str2Int(GetSetting("XQWizard", "Timer", "IncTime", "1"), -60, 600)
Engine_bEngineLog = (Str2Int(GetSetting("XQWizard", "Engine", "EngineLog", "0")) <> 0)
Engine_szLogFile = GetSetting("XQWizard", "Engine", "LogFile", "")
Engine_bUseBook = (Str2Int(GetSetting("XQWizard", "Engine", "UseBook", "1")) <> 0)
Engine_bUseEgtb = (Str2Int(GetSetting("XQWizard", "Engine", "UseEgtb", "1")) <> 0)
Engine_szBookFiles = GetSetting("XQWizard", "Engine", "BookFiles", "")
Engine_szEgtbPaths = GetSetting("XQWizard", "Engine", "EgtbPaths", "")
Engine_nHash = Str2Int(GetSetting("XQWizard", "Engine", "Hash", "0"), 0, 7)
Engine_nThreads = Str2Int(GetSetting("XQWizard", "Engine", "Threads", "0"), 0, 9)
Engine_nRandomness = Str2Int(GetSetting("XQWizard", "Engine", "Randomness", "1"), 0, 3)
Engine_nStyle = Str2Int(GetSetting("XQWizard", "Engine", "Style", "1"), 0, 2)
Engine_szFile = GetSetting("XQWizard", "Engine", "FileName", App_szPath + "ELEEYE.EXE")

End Sub

' 保存档阶员
Public Sub SaveRegs()

SaveSetting "XQWizard", "Options", "LargeGui", IIf(Options_bLargeGui, "1", "0")
SaveSetting "XQWizard", "Options", "Sounds", IIf(Options_bSounds, "1", "0")
SaveSetting "XQWizard", "Options", "AutoPlay", IIf(Options_bAutoPlay, "1", "0")
SaveSetting "XQWizard", "Options", "PlayIntv", LTrim(Str(Options_nPlayIntv))
SaveSetting "XQWizard", "Options", "EscapeDouble", IIf(Options_bEscapeDouble, "1", "0")
SaveSetting "XQWizard", "Options", "AnsiText", IIf(Options_bAnsiText, "1", "0")
SaveSetting "XQWizard", "Options", "MonoBitmap", IIf(Options_bMonoBitmap, "1", "0")
SaveSetting "XQWizard", "Options", "PrintDigit", IIf(Options_bPrintDigit, "1", "0")
SaveSetting "XQWizard", "Options", "FlashXQ", LTrim(Str(Options_nFlashXQ))
SaveSetting "XQWizard", "Options", "MoveDelay", LTrim(Str(Options_nMoveDelay))
SaveSetting "XQWizard", "Options", "AutoFlip", IIf(Options_bAutoFlip, "1", "0")
SaveSetting "XQWizard", "Options", "EngineName", IIf(Options_bEngineName, "1", "0")
SaveSetting "XQWizard", "Options", "AlwaysPonder", IIf(Options_bAlwaysPonder, "1", "0")
SaveSetting "XQWizard", "Options", "AllowResign", IIf(Options_bAllowResign, "1", "0")
SaveSetting "XQWizard", "Options", "EngineTime", IIf(Options_bEngineTime, "1", "0")
SaveSetting "XQWizard", "Options", "StatusBar", IIf(Options_bStatusBar, "1", "0")
SaveSetting "XQWizard", "Options", "EnginePlay", LTrim(Str(Options_nEnginePlay))
SaveSetting "XQWizard", "Options", "BoardSmall", LTrim(Str(Options_nBoardS))
SaveSetting "XQWizard", "Options", "PiecesSmall", LTrim(Str(Options_nPiecesS))
SaveSetting "XQWizard", "Options", "BoardLarge", LTrim(Str(Options_nBoardL))
SaveSetting "XQWizard", "Options", "PiecesLarge", LTrim(Str(Options_nPiecesL))
SaveSetting "XQWizard", "Options", "Music", LTrim(Str(Options_nMusic))
SaveSetting "XQWizard", "Options", "TopMost", IIf(Options_bTopMost, "1", "0")
SaveSetting "XQWizard", "Options", "PromptSave", IIf(Options_bPromptSave, "1", "0")
SaveSetting "XQWizard", "Options", "AutoSave", IIf(Options_bAutoSave, "1", "0")
SaveSetting "XQWizard", "Options", "User", Options_szUser
SaveSetting "XQWizard", "Options", "ShowNew", IIf(Options_bShowNew, "1", "0")
SaveSetting "XQWizard", "Gui", "Left", LTrim(Str(Gui_nLeft))
SaveSetting "XQWizard", "Gui", "Top", LTrim(Str(Gui_nTop))
SaveSetting "XQWizard", "Gui", "EndgamesIndex", LTrim(Str(Gui_nEndgamesIndex))
SaveSetting "XQWizard", "Gui", "EpdName", Gui_szEpdName
SaveSetting "XQWizard", "Gui", "EpdTitle", Gui_szEpdTitle
SaveSetting "XQWizard", "Gui", IIf(App_bPromotion, "ShowHowTo2", "ShowHowTo"), IIf(Gui_bShowHowTo, "1", "0")
SaveSetting "XQWizard", "Gui", "TipIndex", LTrim(Str(Gui_nTipIndex))
SaveSetting "XQWizard", "Gui", "UpgradeVer", Gui_szUpgradeVer
SaveSetting "XQWizard", "Gui", "AdvertDate", Gui_szAdvertDate
SaveSetting "XQWizard", "Publish", "Size", LTrim(Str(Publish_nSize))
SaveSetting "XQWizard", "Publish", "Trad", IIf(Publish_bTrad, "1", "0")
SaveSetting "XQWizard", "Publish", "BoardS", LTrim(Str(Publish_nBoardS))
SaveSetting "XQWizard", "Publish", "PiecesS", LTrim(Str(Publish_nPiecesS))
SaveSetting "XQWizard", "Publish", "BoardL", LTrim(Str(Publish_nBoardL))
SaveSetting "XQWizard", "Publish", "PiecesL", LTrim(Str(Publish_nPiecesL))
SaveSetting "XQWizard", "Publish", "Content", IIf(Publish_bContent, "1", "0")
SaveSetting "XQWizard", "Publish", "Location", LTrim(Str(Publish_nLocation))
SaveSetting "XQWizard", "Publish", "Format", LTrim(Str(Publish_nFormat))
SaveSetting "XQWizard", "Timer", "Depth", LTrim(Str(Timer_nDepth))
SaveSetting "XQWizard", "Timer", "IncMode", IIf(Timer_bIncMode, "1", "0")
SaveSetting "XQWizard", "Timer", "InitTime", LTrim(Str(Timer_nInitTime))
SaveSetting "XQWizard", "Timer", "IncMove", LTrim(Str(Timer_nIncMove))
SaveSetting "XQWizard", "Timer", "IncTime", LTrim(Str(Timer_nIncTime))
SaveSetting "XQWizard", "Engine", "EngineLog", IIf(Engine_bEngineLog, "1", "0")
SaveSetting "XQWizard", "Engine", "LogFile", Engine_szLogFile
SaveSetting "XQWizard", "Engine", "UseBook", IIf(Engine_bUseBook, "1", "0")
SaveSetting "XQWizard", "Engine", "UseEgtb", IIf(Engine_bUseEgtb, "1", "0")
SaveSetting "XQWizard", "Engine", "BookFiles", Engine_szBookFiles
SaveSetting "XQWizard", "Engine", "EgtbPaths", Engine_szEgtbPaths
SaveSetting "XQWizard", "Engine", "Hash", LTrim(Str(Engine_nHash))
SaveSetting "XQWizard", "Engine", "Threads", LTrim(Str(Engine_nThreads))
SaveSetting "XQWizard", "Engine", "Randomness", LTrim(Str(Engine_nRandomness))
SaveSetting "XQWizard", "Engine", "Style", LTrim(Str(Engine_nStyle))
SaveSetting "XQWizard", "Engine", "FileName", Engine_szFile

End Sub

' 以上是翟O置例程

' @示件名Q
Public Sub SetTitle()

Dim sz As String
sz = IIf(App_szFileName = "", "", Mid(App_szFileName, InStrRev(App_szFileName, "\") + 1))
If UCase(Right(sz, 4)) = ".PGN" Then
    sz = Left(sz, Len(sz) - 4)
End If
sz = IIf(sz = "", IIf(Engine_szName = "ElephantEye", "", Engine_szName), sz)
App_frmMain.Caption = IIf(sz = "", "", sz + " - ") + IIf(App_bPromotion, "超象棋巫", "象棋巫")

End Sub

' z查更新
Public Sub CheckUpdate(Optional ByVal bManual As Boolean = False)

Dim sz As String, szUserID As String, szWhatsNew As String, szUrl As String
Dim dwCounter(0 To 1) As Long

sz = ""
QueryPerformanceCounter VarPtr(dwCounter(0))
szUserID = GetSetting("XQWizard", "Gui", "UserID", Hex(dwCounter(0)) + Hex(dwCounter(1)))
On Error Resume Next
sz = frmHide.inet.OpenURL("http://mirror.elephantbase.net/xqwizard/version_trad.php?referer=xqwizard&version=" + _
        STRING_VERSION + "&userid=" + szUserID)
On Error GoTo 0
If sz = "" Then
    If bManual Then
        MsgBox "o法L www.elephantbase.net，稍後再。", vbExclamation
    End If
Else
    Gui_szUpgradeDate = Date ' 如果L升信息成功，那么天就不再L升信息
    SaveSetting "XQWizard", "Gui", "UpgradeDate", Gui_szUpgradeDate ' 立即注员恚避免重_窗口е碌闹匮}L
    SaveSetting "XQWizard", "Gui", "UserID", szUserID               ' 同上
    If Val(sz) > Val(STRING_VERSION) + 0.005 And (bManual Or Val(sz) > Val(Gui_szUpgradeVer) + 0.005) Then
        szWhatsNew = ""
        On Error Resume Next
        szWhatsNew = frmHide.inet.OpenURL("http://mirror.elephantbase.net/xqwizard/whatsnew_trad.txt")
        On Error GoTo 0
        If MsgBox(IIf(szWhatsNew = "", "象棋巫 " + sz + " 已l布，需要下d最新版本幔", szWhatsNew), vbQuestion + vbYesNo) = vbYes Then
            szUrl = ""
            On Error Resume Next
            szUrl = frmHide.inet.OpenURL("http://mirror.elephantbase.net/xqwizard/download_trad.txt")
            On Error GoTo 0
            If Left(szUrl, 7) <> "http://" Then
                szUrl = "http://www.elephantbase.net/xqwizard/download.htm"
            End If
            ShellExecuteA 0, vbNullString, szUrl, vbNullString, vbNullString, SW_SHOWNORMAL
        Else
            Gui_szUpgradeVer = sz
        End If
    Else
        If bManual Then
            MsgBox "您的“象棋巫”已是最新版本了。", vbInformation
        End If
    End If
End If

End Sub

' 退弹
Public Sub ExitPopup()

Dim dfThisTime As Double, dfLastTime As Double
' 用到时才读注册表，避免重开窗口导致的重复访问。这里的做法与Gui_szAdvertDate相反，但达到的目的是一样的。
If GetSetting("XQWizard", "Gui", "PopupDate", "") = Date Then
    Exit Sub
End If
' 如果退弹成功，那么当天就不再退弹了
SaveSetting "XQWizard", "Gui", "PopupDate", Date
frmHide.web.Navigate "http://www.elephantbase.net/advert/popup_trad.htm"
' 退弹调用了frmHide.web，为保证成功，等待1秒钟
dfLastTime = Timer
Do
    DoEvents
    Sleep 1
    dfThisTime = Timer
    dfThisTime = dfThisTime + IIf(dfThisTime < dfLastTime, 86400#, 0#)
Loop Until dfThisTime > dfLastTime + 5#

End Sub

' 以下是主函道程

Public Sub Main()

Dim dfThisTime As Double, nEventIndex As Integer, nLogFileNo As Integer
Dim hWnd As Long, i As Integer, bNewGame As Boolean, sz As String
Dim imgLoading As Picture

' 出赢面，初始化
Randomize Timer
App_bPrevInstance = App.PrevInstance
App_szPath = App.Path + IIf(Right(App.Path, 1) = "\", "", "\")
frmStartup.Show
frmStartup.Refresh
CchessInit CCHESS_TRADITIONAL
' 判嗍欠裨试S升
If UCase(Left(Command, 2)) = "/P" Or UCase(Left(Command, 2)) = "-P" Then
    CchessPromotion 1
    App_bPromotion = True
    sz = Mid(Command, 4)
Else
    CchessPromotion 0
    App_bPromotion = False
    sz = Command
End If
EccoInitOpenVar CCHESS_TRADITIONAL
ConvInit
LoadRegs
LoadTips
If Options_bLargeGui Then
    Set App_frmMain = frmMainLarge
    For i = 0 To 31
        Set App_picPieces(i) = frmHide.imgPiecesLarge(i)
    Next
    Set App_picEngineRed = frmHide.imgEngineRedLarge.Picture
    Set App_picEngineBlack = frmHide.imgEngineBlackLarge.Picture
    BoardSettingLarge Options_nBoardL
    PiecesSettingLarge Options_nPiecesL
    ' 初始化 frmHide 中的小棋P和小棋子，@些D片被 frmPosEdit 用到
    BoardSettingSmallHide Options_nBoardS
    PiecesSettingSmallHide Options_nPiecesS
Else
    Set App_frmMain = frmMainSmall
    For i = 0 To 31
        Set App_picPieces(i) = frmHide.imgPiecesSmall(i)
    Next
    Set App_picEngineRed = frmHide.imgEngineRed.Picture
    Set App_picEngineBlack = frmHide.imgEngineBlack.Picture
    BoardSettingSmall Options_nBoardS
    PiecesSettingSmall Options_nPiecesS
End If
MusicSetting Options_nMusic
Timer_bEnabled = False
' 加d引擎，可能有等待，赢面保持
Engine_nStatus = IDLE_UNLOAD
If Engine_bEngineLog And Engine_szLogFile <> "" Then
    nLogFileNo = FreeFile
    On Error Resume Next
    Open Engine_szLogFile For Output As #nLogFileNo
    Close #nLogFileNo
    On Error GoTo 0
End If
OpenEngine
App_bRedTurn = False
bNewGame = False
If sz = "" Then
    App_szFileName = ""
    If Options_bAutoSave Then
        ReadFile AUTO_SAVE
    Else
        NewGame0
        ' 日e月累窗口P]後，才能@示“_始局”窗口
        bNewGame = True
    End If
Else
    If UCase(Left(sz, 3)) = "/S " Or UCase(Left(sz, 3)) = "-S " Then
        sz = Mid(sz, 4)
        If Left(sz, 1) = """" And Right(sz, 1) = """" Then
            sz = Mid(sz, 2, Len(sz) - 2)
        End If
        LoadFenStr sz
    ElseIf UCase(Left(sz, 3)) = "/F " Or UCase(Left(sz, 3)) = "-F " Then
        sz = Mid(sz, 4)
        If Left(sz, 1) = """" And Right(sz, 1) = """" Then
            sz = Mid(sz, 2, Len(sz) - 2)
        End If
        LoadFenFile sz
    Else
        App_szFileName = sz
        If Left(App_szFileName, 1) = """" And Right(App_szFileName, 1) = """" Then
            App_szFileName = Mid(App_szFileName, 2, Len(App_szFileName) - 2)
        End If
        ReadFile
    End If
End If
' 加dV告，如果加d失。t下dV告列表，再次加d失t放
Gui_dfLastTime = 0#
Gui_nAdvertIndex = 0
Gui_nAdvertTimes(0) = 10 ' 10秒後_始播放V告
LoadAdvertList
If Gui_nAdvertNum = 0 Then
    DownloadAdvertList "advert_trad.txt"
    LoadAdvertList
    If Gui_nAdvertNum = 0 Then
        Gui_nAdvertNum = 1
        Gui_szAdvertFiles(1) = "elephantbase"
        Gui_nAdvertTimes(1) = 32767
    Else
        Gui_szAdvertDate = Date
    End If
End If
' 初始化 frmHide.web
frmHide.web.Navigate "about:blank"
' z查更新，如果用Q定更新，出下d面，出F在象棋巫面之前
If Gui_szUpgradeDate <> Date Then
    CheckUpdate
End If
' @示并{整主窗口
If Not App_bPrevInstance Then
    App_frmMain.Left = Gui_nLeft
    App_frmMain.Top = Gui_nTop
End If
App_frmMain.Show
App_frmMain.web.Navigate2 App_szPath + "ADVERT\elephantbase.htm"
App_frmMain.txtMove.SetFocus
App_frmMain.tlb.ImageList = frmHide.imglst
For i = 1 To 29
    App_frmMain.tlb.Buttons(i).Image = i
Next
If Not App_bPromotion Then
    App_frmMain.mnMovePromote.Visible = False
    App_frmMain.mnMoveSep1.Visible = False
    App_frmMain.tlb.Buttons("MovePromote").Visible = False
End If
App_frmMain.mnOptionsTopMost.Checked = Options_bTopMost
Unload frmStartup
If Gui_bShowHowTo Then
    frmHowTo.Show vbModal, App_frmMain
End If
' 日e月累窗口P]後，才能@示“_始局”窗口
If bNewGame Then
    NewGame
End If

' M入用B，接收事件
nEventIndex = EVENT_TIMER
App_bRunning = True
Do While App_bRunning
    ' 始K把接收引擎的事件放在第一位(高优先)
    If Not ReceiveEngine Then
        ' D循h，依次zy以下几事件
        Select Case nEventIndex
        Case EVENT_TIMER ' rg
            If Timer_bEnabled And Not Timer_bOver(Game_sdMax) Then
                dfThisTime = Timer
                dfThisTime = dfThisTime + IIf(dfThisTime < Timer_dfLastTime, 86400#, 0#)
                If dfThisTime > Timer_dfLastTime + 1# Then
                    Timer_dfLastTime = Timer
                    Timer_nTimeLeft(Game_sdMax) = Timer_nTimeLeft(Game_sdMax) - 1
                    Timer_nTimeElapsed(Game_sdMax) = Timer_nTimeElapsed(Game_sdMax) + 1
                    If Timer_nTimeElapsed(Game_sdMax) > 28800 Then
                        Timer_nTimeElapsed(Game_sdMax) = 28800
                    End If
                    If Timer_nTimeLeft(Game_sdMax) < 0 Then
                        MsgBox IIf(Game_sdMax = 0, "t方超r作！", "黑方超r作！"), vbInformation
                        Game_nResult = IIf(Game_sdMax = 0, RESULT_BLACKWIN, RESULT_REDWIN)
                        Timer_bOver(Game_sdMax) = True
                        Timer_nTimeLeft(Game_sdMax) = 0
                        App_frmMain.lblResult = ResultString
                    End If
                    SetTimer
                End If
            End If
            nEventIndex = EVENT_PLAY
        Case EVENT_PLAY ' 播放棋局
            If App_bPlay Then
                dfThisTime = Timer
                dfThisTime = dfThisTime + IIf(dfThisTime < Timer_dfLastTime, 86400#, 0#)
                If dfThisTime > Timer_dfLastTime + Options_nPlayIntv Then
                    Timer_dfLastTime = Timer
                    MoveForward
                    MoveFlush
                End If
            End If
            nEventIndex = EVENT_ENGINE
        Case EVENT_ENGINE ' 引擎(是否_始思考了)
            If Engine_nStatus = IDLE_READY Then
                If IIf(Game_sdMax = 0, Engine_bRed, Engine_bBlack) Then
                    If Game_nCurrMove < Game_nMaxMove Then
                        Do While Game_nCurrMove < Game_nMaxMove
                            MoveForward
                        Loop
                        MoveFlush
                    End If
                    Engine_nStatus = BUSY_THINK
                    RunEngine
                ElseIf Engine_bAnalyze And Not (Engine_bRed Or Engine_bBlack) Then
                    Engine_nStatus = BUSY_ANALYZE
                    RunEngine
                End If
            End If
            nEventIndex = EVENT_MUSIC
        Case EVENT_MUSIC ' 音
            If Options_nMusic < MUSIC_NONE And Not App_bPrevInstance Then
                If frmHide.mci.Length = frmHide.mci.Position Then
                    frmHide.mci.To = 0
                    frmHide.mci.Command = "Seek"
                    frmHide.mci.Command = "Play"
                End If
            End If
            nEventIndex = EVENT_ADVERT
        Case EVENT_ADVERT ' V告
            ' 播放V告可以跟引擎和播放棋局同rM行，所以不能用 Timer_dfLastTime，而是用 Gui_dfLastTime
            dfThisTime = Timer
            dfThisTime = dfThisTime + IIf(dfThisTime < Gui_dfLastTime, 86400#, 0#)
            If dfThisTime > Gui_dfLastTime + Gui_nAdvertTimes(Gui_nAdvertIndex) Then
                Gui_dfLastTime = Timer
                Gui_nAdvertIndex = Gui_nAdvertIndex + 1
                If Gui_nAdvertIndex > Gui_nAdvertNum Then
                    Gui_nAdvertIndex = 1
                End If
                App_frmMain.web.Navigate2 App_szPath + "ADVERT\" + Gui_szAdvertFiles(Gui_nAdvertIndex) + ".htm"
            End If
            nEventIndex = EVENT_GUI
        Case EVENT_GUI ' 界面
            DoEvents
            nEventIndex = EVENT_TIMER
        End Select
        Sleep 1
    End If
Loop
' 退出前下dV告列表
If Gui_szAdvertDate <> Date Then
    DownloadAdvertList "advert_trad.txt"
    LoadAdvertList
    If Gui_nAdvertNum > 0 Then
        Gui_szAdvertDate = Date
    End If
End If
SaveRegs
PlaySoundA vbNullString, 0, 0
frmHide.mci.Command = "Close"
ExitPopup
Unload frmHide

End Sub

' 以上是主函道程
