Attribute VB_Name = "mdlXQBooth"
' XQBOOTH.BAS - Source Code for XiangQi Booth, Part I
'
' XiangQi Booth - a Little Chinese Chess Challenge Game
' Designed by Morning Yellow, Version: 3.92, Last Modified: Feb. 2009
' Copyright (C) 2004-2008 www.elephantbase.net
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 2 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License along
' with this program; if not, write to the Free Software Foundation, Inc.,
' 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.

Option Explicit

' 全局常量
Public Const STRING_VERSION     As String = "3.92"
Public Const STRING_COPYRIGHT   As String = "(C) 2004-2009 www.elephantbase.net"
Public Const QUOTE_NUM          As Integer = 12     ' 名言l目
Public Const ICON_QUOTE         As Integer = 2      ' 名言D
Public Const MAX_ADVERTS        As Integer = 100    ' V告的最大l目
Public Const MAX_ENDGAMES       As Integer = 4096   ' 局的最大l目
Public Const ENDGAME_FILE       As String = "ENDGAMES\BOOTH.EPD"    ' 局文件
Public Const ENGINE_FILE        As String = "ELEEYE.EXE"    ' 引擎文件
Public Const MANUAL_UPDATE      As Boolean = True   ' CheckUpdate
' 全局常量――棋P(小)O置x
Public Const BOARD_WOOD       As Integer = 0
Public Const BOARD_GREEN      As Integer = 1
Public Const BOARD_WHITE      As Integer = 2
Public Const BOARD_SHEET      As Integer = 3
Public Const BOARD_CANVAS     As Integer = 4
Public Const BOARD_DROPS      As Integer = 5
Public Const BOARD_CLOUDS     As Integer = 6
Public Const BOARD_XQSTUDIO   As Integer = 7
Public Const BOARD_MOVESKY    As Integer = 8
Public Const BOARD_MRSJ       As Integer = 9
Public Const BOARD_ZMBL       As Integer = 10
Public Const BOARD_QIANHONG   As Integer = 11
Public Const BOARD_MAX        As Integer = 12
' 全局常量――棋子(小)O置x
Public Const PIECES_WOOD      As Integer = 0
Public Const PIECES_DELICATE  As Integer = 1
Public Const PIECES_POLISH    As Integer = 2
Public Const PIECES_XQSTUDIO  As Integer = 3
Public Const PIECES_MOVESKY   As Integer = 4
Public Const PIECES_MRSJ      As Integer = 5
Public Const PIECES_ZMBL      As Integer = 6
Public Const PIECES_MAX       As Integer = 7

' 全局量
Public App_szPath               As String   ' 主程序所在的文件A，以"\"Y尾
Public App_bRunning             As Boolean  ' 程序\行酥荆rO成True，如果是False，t跳出事件zy循h
Public App_nEndgames            As Integer  ' x入的局盗
Public App_szEndgames(0 To MAX_ENDGAMES - 1) As String  ' x入的局
Public Gui_nBoard               As Integer  ' 棋P
Public Gui_nPieces              As Integer  ' 棋子
Public Gui_bSounds              As Boolean  ' 音效
Public Gui_szUpgradeVer         As String   ' z查升的版本
Public Gui_szUpgradeDate        As String   ' z查升的日期
Public Gui_nCurr                As Integer  ' 前局的l目序
Public Gui_nLast                As Integer  ' 最后局的l目序
Public Gui_szAdvertDate         As String   ' z查V告的日期
Public Gui_dfLastTime           As Double   ' 前rg(每天00:00^去的秒)
Public Gui_nAdvertNum           As Integer  ' V告盗
Public Gui_nAdvertIndex         As Integer  ' 前V告序
Public Gui_szAdvertFiles(1 To MAX_ADVERTS) As String ' 每V告的文件
Public Gui_nAdvertTimes(0 To MAX_ADVERTS) As Integer ' 每V告的rg(秒)
Public Gui_szQuotes(1 To QUOTE_NUM) As String ' “名言”的l目
Public Engine_pipe          As PipeStruct   ' 引擎管道

' 下dV告列表
Public Sub DownloadAdvertList()

Dim buffer() As Byte, nFileNo As Integer, sz As String
nFileNo = FreeFile
On Error GoTo lnErrorOpen
sz = frmHide.inet.OpenURL("http://www.elephantbase.net/xqwizard/advert.txt")
On Error GoTo 0
If sz <> "" Then
    On Error Resume Next
    MkDir App_szPath + "ADVERT"
    On Error GoTo lnErrorOpen
    Open App_szPath + "ADVERT\advert.txt" For Output As #nFileNo
    On Error GoTo 0
    sz = Replace(sz, Chr(10), vbCrLf)
    sz = Replace(sz, Chr(13) + vbCrLf, vbCrLf)
    Print #nFileNo, sz;
    Close #nFileNo
End If

lnErrorOpen:
On Error GoTo 0

End Sub

' @取文件L度
Public Function GetFileSize(ByVal szFileName As String) As Long

Dim nFileNo As Integer
GetFileSize = 0
nFileNo = FreeFile
On Error GoTo lnErrorOpen
Open szFileName For Binary Access Read As #nFileNo
On Error GoTo 0
GetFileSize = LOF(nFileNo)
Close #nFileNo

lnErrorOpen:
On Error GoTo 0

End Function

' z查V告(htm和gif文件)
Public Sub CheckAdvert(ByVal szFileName As String, ByVal nHtmSize As Long, ByVal nGifSize As Long)

Dim nFileNo As Integer, sz As String, buffer() As Byte
' 判辔募字担如果跟V告列表中的一致，就不需要下d了
If GetFileSize(App_szPath + "ADVERT\" + szFileName + ".htm") = nHtmSize Then
    If GetFileSize(App_szPath + "ADVERT\" + szFileName + ".gif") = nGifSize Then
        Exit Sub
    End If
End If

nFileNo = FreeFile
' 下dhtm文件，文本方式
On Error GoTo lnErrorOpen
sz = frmHide.inet.OpenURL("http://www.elephantbase.net/xqwizard/" + szFileName + ".htm")
On Error GoTo 0
If sz <> "" Then
    On Error GoTo lnErrorOpen
    Open App_szPath + "ADVERT\" + szFileName + ".htm" For Output As #nFileNo
    On Error GoTo 0
    sz = Replace(sz, Chr(10), vbCrLf)
    sz = Replace(sz, Chr(13) + vbCrLf, vbCrLf)
    Print #nFileNo, sz;
    Close #nFileNo
End If
' 下dgif文件，二M制方式
On Error GoTo lnErrorOpen
buffer() = frmHide.inet.OpenURL("http://www.elephantbase.net/xqwizard/" + szFileName + ".gif", icByteArray)
On Error GoTo 0
If UBound(buffer) >= 0 Then
    On Error Resume Next
    Kill App_szPath + "ADVERT\" + szFileName + ".gif"
    On Error GoTo lnErrorOpen
    Open App_szPath + "ADVERT\" + szFileName + ".gif" For Binary Access Write As #nFileNo
    On Error GoTo 0
    Put #nFileNo, , buffer()
    Close #nFileNo
End If

lnErrorOpen:
On Error GoTo 0

End Sub

' 加dV告列表
Public Sub LoadAdvertList()

Dim nFileNo As Integer, sz As String, szs() As String
' z查V告文件是否有热
Gui_nAdvertNum = 0
nFileNo = FreeFile
On Error GoTo lnErrorOpen
Open App_szPath + "ADVERT\advert.txt" For Input As #nFileNo
On Error GoTo 0
If EOF(nFileNo) Then
    Close #nFileNo
    Exit Sub
End If
' z查V告文件是否正确
Line Input #nFileNo, sz
If sz <> "XQWizard Advert List" Then
    Close #nFileNo
    Exit Sub
End If
' 逐一x取V告
Do While Not EOF(nFileNo)
    Line Input #nFileNo, sz
    ' 每一行包含的信息有：文件名，播放rg，htm文件大小，gif文件大小
    ' 四用逗隔_，如“elephantbase,10,183,5618”
    szs = Split(sz, ",")
    If UBound(szs) >= 0 Then
        Gui_nAdvertNum = Gui_nAdvertNum + 1
        Gui_szAdvertFiles(Gui_nAdvertNum) = szs(0)
        If UBound(szs) >= 1 Then
            Gui_nAdvertTimes(Gui_nAdvertNum) = Str2Int(szs(1))
            If UBound(szs) >= 3 Then
                CheckAdvert szs(0), Str2Lng(szs(2)), Str2Lng(szs(3))
            End If
        Else
            Gui_nAdvertTimes(Gui_nAdvertNum) = 10
        End If
    End If
Loop
Close #nFileNo

lnErrorOpen:
On Error GoTo 0

End Sub

' 淖员砑虞d
Public Sub LoadRegs()

Gui_szUpgradeVer = GetSetting("XQWizard", "Gui", "UpgradeVer", STRING_VERSION)
Gui_szUpgradeDate = GetSetting("XQWizard", "Gui", "UpgradeDate", "")
Gui_szAdvertDate = GetSetting("XQWizard", "Gui", "AdvertDate", "")
Gui_nBoard = Str2Int(GetSetting("XQBooth", "Gui", "Board", "0"), 0, BOARD_MAX - 1)
Gui_nPieces = Str2Int(GetSetting("XQBooth", "Gui", "Pieces", "0"), 0, PIECES_MAX - 1)
Gui_bSounds = GetSetting("XQBooth", "Gui", "Sounds", "1") <> "0"
Gui_nLast = Str2Int(GetSetting("XQBooth", "Gui", "Last", "0"), 0, MAX_ENDGAMES - 1)
Gui_nCurr = Str2Int(GetSetting("XQBooth", "Gui", "Curr", "0"), 0, MAX_ENDGAMES - 1)

End Sub

' 保存档阶员
Public Sub SaveRegs()

SaveSetting "XQWizard", "Gui", "UpgradeVer", Gui_szUpgradeVer
SaveSetting "XQWizard", "Gui", "UpgradeDate", Gui_szUpgradeDate
SaveSetting "XQWizard", "Gui", "AdvertDate", Gui_szAdvertDate
SaveSetting "XQBooth", "Gui", "Board", LTrim(Str(Gui_nBoard))
SaveSetting "XQBooth", "Gui", "Pieces", LTrim(Str(Gui_nPieces))
SaveSetting "XQBooth", "Gui", "Sounds", IIf(Gui_bSounds, "1", "0")
SaveSetting "XQBooth", "Gui", "Last", LTrim(Str(Gui_nLast))
SaveSetting "XQBooth", "Gui", "Curr", LTrim(Str(Gui_nCurr))

End Sub

' z查更新
Public Sub CheckUpdate(Optional ByVal bManual As Boolean = False)

Dim sz As String, szWhatsNew As String, szUrl As String
sz = ""
On Error Resume Next
sz = frmHide.inet.OpenURL("http://mirror.elephantbase.net/xqwizard/version_trad.php")
On Error GoTo 0
If sz = "" Then
    If bManual Then
        MsgBox "o法L www.elephantbase.net，稍後再。", vbExclamation
    End If
Else
    Gui_szUpgradeDate = Date ' 如果L升信息成功，那么天就不再L升信息
    If Val(sz) > Val(STRING_VERSION) + 0.005 And (bManual Or Val(sz) > Val(Gui_szUpgradeVer) + 0.005) Then
        szWhatsNew = ""
        On Error Resume Next
        szWhatsNew = frmHide.inet.OpenURL("http://mirror.elephantbase.net/xqwizard/whatsnew_trad.txt")
        On Error GoTo 0
        If MsgBox(IIf(szWhatsNew = "", "象棋巫 " + sz + " 已l布，需要下d最新版本幔", szWhatsNew), vbQuestion + vbYesNo) = vbYes Then
            szUrl = ""
            On Error Resume Next
            szUrl = frmHide.inet.OpenURL("http://mirror.elephantbase.net/xqwizard/download_trad.txt")
            On Error GoTo 0
            If Left(szUrl, 7) <> "http://" Then
                szUrl = "http://www.elephantbase.net/xqwizard/download.htm"
            End If
            ShellExecuteA 0, vbNullString, szUrl, vbNullString, vbNullString, SW_SHOWNORMAL
        Else
            Gui_szUpgradeVer = sz
        End If
    Else
        If bManual Then
            MsgBox "您的“象棋巫”已是最新版本了。", vbInformation
        End If
    End If
End If

End Sub

' 加d名言
Public Sub LoadTips()

Gui_szQuotes(1) = "　　子曰：“W而r之，不亦f乎？有朋自h方恚不亦泛酰咳瞬恢而不C，不亦君子乎？”" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《Z・W而》"
Gui_szQuotes(2) = "　　子曰：“有一言而可以K身行之者乎？”子曰：“其恕乎！己所不欲，勿施於人。”" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《Z・l`公》"
Gui_szQuotes(3) = "　　或曰：“以德笤梗何如？”子曰：“何以蟮拢恳灾笤梗以德蟮隆！" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《Z・》"
Gui_szQuotes(4) = "　　子夏曰：“博W而V志，切而近思；仁在其中矣。”" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《Z・子》"
Gui_szQuotes(5) = "　　子曰：“不患人之不己知，患不知人也。”" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《Z・W而》"
Gui_szQuotes(6) = "　　子曰：“三人行，必有我焉：衿渖普叨闹，其不善者而改之。”" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《Z・述而》"
Gui_szQuotes(7) = "　　⒂歙之，必固之；⒂弱之，必固之；⒂U之，必固d之；⒂取之，必固与之。是^微明。" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《老子・第三十六章》"
Gui_szQuotes(8) = "　　柔弱胜。~不可于Y，之利器不可以示人。" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《老子・第三十六章》"
Gui_szQuotes(9) = "　　Dy于其易；榇笥谄浼。天下y事，必作于易；天下大事，必作于。" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《老子・第六十三章》"
Gui_szQuotes(10) = "　　合抱之木，生于毫末；九又_，起于累土；千里之行，始于足下。" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《老子・第六十四章》"
Gui_szQuotes(11) = "　　知彼知己，胜乃不殆；知天知地，胜乃可全。" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《O子兵法・地形》"
Gui_szQuotes(12) = "　　怒可以复喜，C可以复f，亡不可以复存，死者不可以复生。" + vbCrLf + vbCrLf + _
        "　　　　　　　　　　　　　　　　　　　　　　――《O子兵法・火攻》"

End Sub

' 主函
Public Sub Main()

Dim i As Integer, nFileNo As Integer, bLoaded As Boolean
Dim sz As String, lpStr As Long
Dim dfThisTime As Double, dfLastTime As Double

' 初始化
App_szPath = App.Path + IIf(Right(App.Path, 1) = "\", "", "\")
CchessInit
LoadRegs
LoadTips

' 加d局
nFileNo = FreeFile
On Error GoTo lnErrorOpen
Open App_szPath + ENDGAME_FILE For Input As #nFileNo
On Error GoTo 0
App_nEndgames = 0
Do While App_nEndgames < MAX_ENDGAMES And Not EOF(nFileNo)
    Line Input #nFileNo, sz
    i = InStr(sz, ";")
    If i > 0 Then
        App_szEndgames(App_nEndgames) = RTrim(Left(sz, i - 1))
        App_nEndgames = App_nEndgames + 1
    Else
        sz = RTrim(sz)
        If sz <> "" Then
            App_szEndgames(App_nEndgames) = sz
            App_nEndgames = App_nEndgames + 1
        End If
    End If
Loop
Close #nFileNo
If App_nEndgames = 0 Then
    MsgBox "o法打_文件 " + App_szPath + ENDGAME_FILE, vbExclamation
    Exit Sub
End If
If Gui_nLast >= App_nEndgames Then
    Gui_nLast = App_nEndgames - 1
End If
If Gui_nCurr > Gui_nLast Then
    Gui_nCurr = Gui_nLast
End If

' 加d引擎
PipeOpen Engine_pipe, App_szPath + ENGINE_FILE
PipeLineOutput Engine_pipe, "ucci"
dfLastTime = Timer
bLoaded = False
Do While Not bLoaded
    lpStr = PipeLineInput(Engine_pipe)
    If lpStr = 0 Then
        Sleep 1
        dfThisTime = Timer
        dfThisTime = dfThisTime + IIf(dfThisTime < dfLastTime, 86400#, 0#)
        If dfThisTime > dfLastTime + 10# Then
            Exit Do
        End If
    Else
        sz = MkBStr(lpStr)
        If sz = "ucciok" Then
            bLoaded = True
        End If
    End If
Loop
If Not bLoaded Then
    PipeLineOutput Engine_pipe, "quit"
    PipeClose Engine_pipe
    MsgBox "o法加d引擎 " + App_szPath + ENGINE_FILE, vbExclamation
    Exit Sub
End If
' X毯冢不能只考]④走法
' PipeLineOutput Engine_pipe, "setoption alwayscheck true"

' {整V告框大小
On Error Resume Next
frmHide.imgAdvert.Picture = LoadPicture(App_szPath + "ADVERT\elephantbase.gif")
On Error GoTo 0
' 加dV告，如果加d失。t下dV告列表，再次加d失t放
Gui_dfLastTime = 0#
Gui_nAdvertIndex = 0
Gui_nAdvertTimes(0) = 10 ' 10秒后_始播放V告
LoadAdvertList
If Gui_nAdvertNum = 0 Then
    DownloadAdvertList
    LoadAdvertList
    If Gui_nAdvertNum = 0 Then
        Gui_nAdvertNum = 1
        Gui_szAdvertFiles(1) = "elephantbase"
        Gui_nAdvertTimes(1) = 32767
    Else
        Gui_szAdvertDate = Date
    End If
End If
' z查更新，如果用Q定更新，出下d面，出F在象棋巫面之前
If Gui_szUpgradeDate <> Date Then
    CheckUpdate
End If

frmXQBooth.Show
frmXQBooth.mnOptionsSounds.Checked = Gui_bSounds
frmXQBooth.frmWeb.Width = frmHide.imgAdvert.Width
frmXQBooth.frmWeb.Height = frmHide.imgAdvert.Height
frmXQBooth.web.Navigate "file://" + App_szPath + "ADVERT\elephantbase.htm"

' 主循h
App_bRunning = True
Do While App_bRunning
    ' 播放V告可以跟引擎和播放棋局同rM行，所以不能用 Timer_dfLastTime，而是用 Gui_dfLastTime
    dfThisTime = Timer
    dfThisTime = dfThisTime + IIf(dfThisTime < Gui_dfLastTime, 86400#, 0#)
    If dfThisTime > Gui_dfLastTime + Gui_nAdvertTimes(Gui_nAdvertIndex) Then
        Gui_dfLastTime = Timer
        Gui_nAdvertIndex = Gui_nAdvertIndex + 1
        If Gui_nAdvertIndex > Gui_nAdvertNum Then
            Gui_nAdvertIndex = 1
        End If
        frmXQBooth.web.Navigate "file://" + App_szPath + "ADVERT\" + Gui_szAdvertFiles(Gui_nAdvertIndex) + ".htm"
    End If
    DoEvents
    Sleep 1
Loop
' 退出前下dV告列表
If Gui_szAdvertDate <> Date Then
    DownloadAdvertList
    LoadAdvertList
    If Gui_nAdvertNum > 0 Then
        Gui_szAdvertDate = Date
    End If
End If
' P]引擎
PipeLineOutput Engine_pipe, "quit"
dfLastTime = Timer
Do While bLoaded
    lpStr = PipeLineInput(Engine_pipe)
    If lpStr = 0 Then
        Sleep 1
        dfThisTime = Timer
        dfThisTime = dfThisTime + IIf(dfThisTime < dfLastTime, 86400#, 0#)
        If dfThisTime > dfLastTime + 1# Then
            Exit Do
        End If
    Else
        sz = MkBStr(lpStr)
        If sz = "bye" Then
            bLoaded = False
        End If
    End If
Loop
PipeClose Engine_pipe
If bLoaded Then
    MsgBox "o法正常卸d引擎！", vbExclamation
End If
' 退出
SaveRegs
PlaySoundA 0, 0, 0
Unload frmHide

Exit Sub
lnErrorOpen:
On Error GoTo 0
MsgBox "o法打_文件 " + App_szPath + ENDGAME_FILE, vbExclamation

End Sub
